&НаСервере
Процедура ПриСозданииНаСервере(Отказ,СтандартнаяОбработка)

// Заголовок для колонки "Провайдеры.Тип"
//-------------------------------------------------------------------------------------------------
	Если (Объект.Предопределенный = Истина) Тогда
		Элементы.Провайдеры.ПодчиненныеЭлементы.ПровайдерыТип.Заголовок = "Доступно на серверах";
		ЭтаФорма.Заголовок = "Состояние внутреннего телефона";
	Иначе
		Элементы.Провайдеры.ПодчиненныеЭлементы.ПровайдерыТип.Заголовок = "Доступно у провайдеров";
		ЭтаФорма.Заголовок = "Состояние мобильного телефона";
	КонецЕсли;

	ПровайдерыСформировать();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение,ИсточникВыбора)

// Проверим правильность выбранного значения
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Провайдеры"));
	Доступно = Доступно ИЛИ (ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ТелСерверы"));

// Проверим ВыбранноеЗначение на наличие в ТЧ "Провайдеры"
//-------------------------------------------------------------------------------------------------
	Если (Доступно = Истина) Тогда
		Фильтр = Новый Структура;
		Фильтр.Вставить("Провайдер",ВыбранноеЗначение);
		Результат = Объект.Провайдеры.НайтиСтроки(Фильтр);

		Если (Результат.Количество() = 0) Тогда
			НовСтрока = Объект.Провайдеры.Добавить();
			НовСтрока.Провайдер = ВыбранноеЗначение;
			ЭтаФорма.Модифицированность = Истина;
			ПровайдерыСформировать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//-------------------------------------------------------------------------------------------------

&НаСервере
Процедура ПровайдерыСформировать()

// Для каждой строки ТЧ "Провайдеры" сформируем информацию о типе провайдера
//-------------------------------------------------------------------------------------------------
	Для Каждого ТекСтрока Из Объект.Провайдеры Цикл
		Если (ТипЗнч(ТекСтрока.Провайдер) = Тип("СправочникСсылка.Провайдеры")) Тогда
			ТекСтрока.Тип = СокрЛП(ТекСтрока.Провайдер);
		ИначеЕсли (ТипЗнч(ТекСтрока.Провайдер) = Тип("СправочникСсылка.ТелСерверы")) Тогда
			ТекСтрока.Тип = "Asterisk/" + СокрЛП(ТекСтрока.Провайдер);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПровайдерыПередНачаломДобавления(Элемент,Отказ,Копирование,Родитель,Группа,Параметр)

// Если текущий объект является предопределенным элементом, то в качестве провайдера добавим
// внутренний сервер телефонии. Иначе - мобильного провайдера
//-------------------------------------------------------------------------------------------------
	ИмяСправочника = ?(Объект.Предопределенный,"ТелСерверы","Провайдеры");
	ИмяСправочника = "Справочник." + ИмяСправочника + ".ФормаВыбора";
	Отказ = Истина;

// Откроем форму для подбора
//-------------------------------------------------------------------------------------------------
	СтрПараметры = Новый Структура;
	СтрПараметры.Вставить("ЗакрыватьПриВыборе",Ложь);
	ОткрытьФорму(ИмяСправочника,СтрПараметры,ЭтаФорма);
КонецПроцедуры
