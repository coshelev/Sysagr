&НаСервере
Процедура ПриСозданииНаСервере(Отказ,СтандартнаяОбработка)

// Линейный элемент справочника не может находиться вне группы
//-------------------------------------------------------------------------------------------------
	Если (НЕ ЗначениеЗаполнено(Объект.Родитель)) Тогда
		ТекстСообщения = "Панель быстрого вызова должена принадлежать контакт-центру";
		Сообщить(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;

// Сформируем заголовок формы
//-------------------------------------------------------------------------------------------------
	Темп = "Панель для """ + СокрЛП(Объект.Родитель) + """";
	ЭтаФорма.Заголовок = СокрЛП(Темп);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение,ИсточникВыбора)

// В зависимости от типа выбранного значения
//-------------------------------------------------------------------------------------------------
	Если (ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ТелОчереди")) Тогда
		СоставДобавитьНаСервере(ВыбранноеЗначение);
	ИначеЕсли (ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ТелВнутренние")) Тогда
		СоставДобавитьНаСервере(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

//-------------------------------------------------------------------------------------------------

&НаКлиенте
Процедура СоставДобавить(Команда)

// В зависимости от типа добавляемого элемента откроем форму выбора
//-------------------------------------------------------------------------------------------------
	СтрПараметры = Новый Структура("ЗакрыватьПриВыборе",Ложь);
	ФормаИмя = ?(Команда.Имя = "СоставДобавитьОчередь","ТелОчереди","ТелВнутренние");
	ФормаВыбора = ПолучитьФорму("Справочник." + ФормаИмя + ".ФормаВыбора",СтрПараметры,ЭтаФорма);
	ФормаВыбора.Открыть();
КонецПроцедуры

&НаСервере
Процедура СоставДобавитьНаСервере(ТелСсылка)

// Найдем переданную ссылку (внутренний телефон или системную очередь) в табличной части
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ТелСсылка) = Тип("СправочникСсылка.ТелВнутренние"));
	Доступно = Доступно ИЛИ (ТипЗнч(ТелСсылка) = Тип("СправочникСсылка.ТелОчереди"));
	Доступно = Доступно И ЗначениеЗаполнено(ТелСсылка);

	Если (Доступно = Истина) Тогда
		СтрПараметры = Новый Структура("Телефон",ТелСсылка);
		НайденныеСтроки = Объект.ПанельСостав.НайтиСтроки(СтрПараметры);

		Если (НайденныеСтроки.Количество() = 0) Тогда
			НовСтрока = Объект.ПанельСостав.Добавить();
			НовСтрока.Телефон = ТелСсылка;

// Сформируем реквизит "Наименование" для добавленной строки
//-------------------------------------------------------------------------------------------------
			ВладСсылка = Телефония.ВладелецПолучить(ТелСсылка);
			НовСтрока.Наименование = СокрЛП(ВладСсылка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоставПередНачаломДобавления(Элемент,Отказ,Копирование,Родитель,Группа,Параметр)
	СоставДобавить(ЭтаФорма.Команды["СоставДобавитьТелефон"]);
	Отказ = Истина;
КонецПроцедуры
