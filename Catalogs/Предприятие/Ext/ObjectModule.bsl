Процедура ОбработкаЗаполнения(ДанныеЗаполнения,ТекстЗаполнения,СтандартнаяОбработка)

// Если в параметре "ДанныеЗаполнения" содержится структура и в ней есть реквизит "Родитель", то
// попытаемся заполнить некоторые реквизиты нового элемента данными на основании "Родителя"
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ДанныеЗаполнения) = Тип("Структура"));
	Доступно = Доступно И (ДанныеЗаполнения.Свойство("Родитель") = Истина);

	Если (Доступно = Истина) Тогда
		ЭтотОбъект.Родитель = ДанныеЗаполнения.Родитель;
		ЭтотОбъект.Холдер = ДанныеЗаполнения.Родитель.Холдер;
		ЭтотОбъект.ТочкаРазмещения = ДанныеЗаполнения.Родитель.ТочкаРазмещения;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ,ПроверяемыеРеквизиты)

// Если текущий объект является группой
// Если у группы запрещены исходящие внешние звонки, то отключим проверку реквизита "ИсхКанал"
//-------------------------------------------------------------------------------------------------
	Если (ЭтотОбъект.ЭтоГруппа = Истина) Тогда
		Если (ЭтотОбъект.ИсхЗапрет = Истина) Тогда
			ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ИсхКанал"));
			ЭтотОбъект.ИсхЗапись = Ложь;

// Если же у группы разрешены исходящие внешние звонки, но реквизит "ИсхКанал" не заполнен, то
// попытаемся заполнить его из настроек вышестоящей группы
//-------------------------------------------------------------------------------------------------
		ИначеЕсли (НЕ ЗначениеЗаполнено(ЭтотОбъект.ИсхКанал)) Тогда
			ТекРодитель = ЭтотОбъект.Родитель;

			Пока (ЗначениеЗаполнено(ТекРодитель)) Цикл
				Если (ЗначениеЗаполнено(ТекРодитель.ИсхКанал)) Тогда
					ЭтотОбъект.ИсхКанал = ТекРодитель.ИсхКанал;
					Прервать;
				КонецЕсли;

				ТекРодитель = ТекРодитель.Родитель;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

// Если текущий объект - линейный элемент, то убедимся в том, что он принадлежит группе
//-------------------------------------------------------------------------------------------------
	Если (ЭтотОбъект.ЭтоГруппа = Ложь) Тогда
		Если (НЕ ЗначениеЗаполнено(ЭтотОбъект.Родитель)) Тогда
			Сообщить("Линейное рабочее место должно обязательно принадлежать группе (подразделению)");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;

// Выполним нормализацию реквизитов
//-------------------------------------------------------------------------------------------------
	ЭтотОбъект.Наименование = СокрЛП(ЭтотОбъект.Наименование);
	ЭтотОбъект.НаименованиеПолное = СокрЛП(ЭтотОбъект.ПолноеНаименование());

// Если текущий объект является линейным элементом
//-------------------------------------------------------------------------------------------------
	Если (ЭтотОбъект.ЭтоГруппа = Ложь) Тогда
		Если (ЭтотОбъект.Состояние = Перечисления.СостояниеЭлементаПредприятия.РабочееМесто) Тогда
			Темп = СокрЛП(ЭтотОбъект.ПолноеНаименование()) + "/" + СокрЛП(ЭтотОбъект.Должность);
			ЭтотОбъект.Должность = СокрЛП(ЭтотОбъект.Должность);
			ЭтотОбъект.НаименованиеПолное = СокрЛП(Темп);
		Иначе
			ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Должность"));
			ЭтотОбъект.Должность = "";
		КонецЕсли;

		ЭтотОбъект.ТочкаРазмещения = ЭтотОбъект.Родитель.ТочкаРазмещения;
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если (НЕ ЭтотОбъект.ПроверитьЗаполнение()) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Если пытаются переименовать существующий элемент - отказать (только через обработку)
	//-------------------------------------------------------------------------------------
	Доступно_0 = Не ЭтоНовый();
	Доступно_0 = Доступно_0 И НЕ ЭтоГруппа;
	Доступно = Не Константы.РазрешеноПереименованиеВСправочникеПредприятие.Получить() И Доступно_0;  
	
	Если Доступно Тогда
		Если Наименование <> Ссылка.Наименование Тогда
			Сообщить("Переименовывать запрещено");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
// Если текущий элемент является группой, то перезапишем все подчиненные группы и элементы
//-------------------------------------------------------------------------------------------------
	Если (ЭтотОбъект.ЭтоГруппа = Истина) Тогда
		Выборка = Справочники.Предприятие.Выбрать(ЭтотОбъект.Ссылка);

		Пока (Выборка.Следующий()) Цикл
			Выборка.ПолучитьОбъект().Записать();
		КонецЦикла;
	КонецЕсли;
	
// Если запись выполняется при записи сотрудника ОУ, тогда не перезаписывать внутренние телефоны
//-------------------------------------------------------------------------------------------------
	РежимЗагрузкиСотрудниковУО = Ложь;
	Доступно = ДополнительныеСвойства.Свойство("ЗагрузиСотрудниковОУ", РежимЗагрузкиСотрудниковУО);
	Если Доступно Тогда
		Возврат;
	КонецЕсли;

// Перезапишем все подчиненные внутренние телефоны (корректировка функции записи исходящих)
//-------------------------------------------------------------------------------------------------
	СтрПараметры = Новый Структура("Владелец",ЭтотОбъект.Ссылка);
	Выборка = РегистрыСведений.ОбъектыПривязка.Выбрать(СтрПараметры);

	Пока (Выборка.Следующий()) Цикл
		Если (ТипЗнч(Выборка.Объект) = Тип("СправочникСсылка.ТелВнутренние")) Тогда
			// С 18.11.2019 не пишем в Abonents
			//Выборка.Объект.ПолучитьОбъект().Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
