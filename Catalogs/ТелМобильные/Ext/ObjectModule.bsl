Процедура ОбработкаПроверкиЗаполнения(Отказ,ПроверяемыеРеквизиты)

// Нормализация реквизитов
//-------------------------------------------------------------------------------------------------
	ЭтотОбъект.Код = СокрЛП(ЭтотОбъект.Код);
	ЭтотОбъект.Наименование = СокрЛП(ЭтотОбъект.Код);
	ЭтотОбъект.НомерКарты = СокрЛП(ЭтотОбъект.НомерКарты);

// Проверка на дублирование по НомеруКарты
//-------------------------------------------------------------------------------------------------
	Если (ЗначениеЗаполнено(ЭтотОбъект.НомерКарты)) Тогда
		ТекстЗапроса = "
		|SELECT	COUNT(*) AS Всего
		|FROM	Справочник.ТелМобильные
		|WHERE	(Ссылка <> &ЭтотОбъект) И (НомерКарты = &НомерКарты)";

		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ЭтотОбъект",ЭтотОбъект.Ссылка);
		Запрос.УстановитьПараметр("НомерКарты",ЭтотОбъект.НомерКарты);
		Результат = Запрос.Выполнить().Выбрать();

		Если (Результат.Следующий()) И (Результат.Всего > 0) Тогда
			ТекстСообщения = "SIM-карта с номером [" + СокрЛП(ЭтотОбъект.НомерКарты) + "] уже присутствует в справочнике";
			Сообщить(ТекстСообщения);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)

// Найдем внутренний телефон - конвергентную пару
// Если пара найдена, то выполним отключение объекта от конвергенции
// Выполним отвязку объекта от текущего владельца
//-------------------------------------------------------------------------------------------------
	ПараФмс = Телефония.КонвергенцияПолучить(ЭтотОбъект.Ссылка);
	Телефония.КонвергенцияРегистрУдалить(ПараФмс);
	Телефония.ВладелецОчистить(ЭтотОбъект.Ссылка);

// Очистим сведения о начислениях за услуги мобильной связи
//-------------------------------------------------------------------------------------------------
	НаборЗаписей = РегистрыСведений.УслугиМобильные.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Телефон.Установить(ЭтотОбъект.Ссылка);
	НаборЗаписей.Записать();
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если (НЕ ЭтотОбъект.ПроверитьЗаполнение()) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

// Если текущий объект не новый, у него есть конвергентная пара и у него сменился реквизит
// "ТипФМС", то перезапишем конвергентный внутренний телефон
//-------------------------------------------------------------------------------------------------
	Доступно = ЗначениеЗаполнено(ЭтотОбъект.Ссылка);
	Доступно = Доступно И (ЭтотОбъект.Ссылка.ТипФМС <> ЭтотОбъект.ТипФМС);

	Если (Доступно = Истина) Тогда
		ПараФМС = Телефония.КонвергенцияПолучить(ЭтотОбъект.Ссылка);

		Если (ЗначениеЗаполнено(ПараФМС)) Тогда
			ЭтотОбъект.ДополнительныеСвойства.Вставить("ПараФМС",ПараФМС);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)

// Если в структуре "ДополнительныеСвойства" передана информация о конвергентной паре, значит
// есть необходимость в перезаписи конвергентного внутреннего телефона
//-------------------------------------------------------------------------------------------------
	Доступно = (ЭтотОбъект.ДополнительныеСвойства.Свойство("ПараФМС") = Истина);
	Доступно = Доступно И ЗначениеЗаполнено(ЭтотОбъект.ДополнительныеСвойства.ПараФмс);

	Если (Доступно = Истина) Тогда
		СпрСсылка = ЭтотОбъект.ДополнительныеСвойства.ПараФмс;
		СпрСсылка.ПолучитьОбъект().Записать();
	КонецЕсли;
КонецПроцедуры
