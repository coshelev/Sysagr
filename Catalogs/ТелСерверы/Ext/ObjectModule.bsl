Процедура ОбработкаПроверкиЗаполнения(Отказ,ПроверяемыеРеквизиты)

// Если сервер не является управляемым, то отключим проверку некоторых реквизитов
//-------------------------------------------------------------------------------------------------
	Если (НЕ ЗначениеЗаполнено(ЭтотОбъект.ИсточникДанных)) Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ГрафикРаботы"));
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("КаталогХранилища"));
	КонецЕсли;

// Нормализация реквизитов
//-------------------------------------------------------------------------------------------------
	ЭтотОбъект.Наименование = СокрЛП(ЭтотОбъект.Наименование);
	ЭтотОбъект.ИсточникДанных = СокрЛП(ЭтотОбъект.ИсточникДанных);
	ЭтотОбъект.КаталогХранилища = ВРег(СокрЛП(ЭтотОбъект.КаталогХранилища));
	ЭтотОбъект.IPАдрес = ВРег(СокрЛП(СтрЗаменить(ЭтотОбъект.IPАдрес," ","")));
	ЭтотОбъект.IPАдресХранилища = ВРег(СокрЛП(СтрЗаменить(ЭтотОбъект.IPАдресХранилища," ","")));

// Проверка на дублирование по Наименованию
//-------------------------------------------------------------------------------------------------
	ТекстЗапроса = "
	|SELECT	COUNT(*) AS Всего
	|FROM	Справочник.ТелСерверы
	|WHERE	(Ссылка <> &ЭтотОбъект) И (Наименование = &Наименование)";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЭтотОбъект",ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Наименование",ЭтотОбъект.Наименование);
	Результат = Запрос.Выполнить().Выбрать();

	Если (Результат.Следующий()) И (Результат.Всего > 0) Тогда
		ТекстСообщения = "Сервер с наименованием """ + СокрЛП(ЭтотОбъект.Наименование) + """ уже присутствует в справочнике";
		Сообщить(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;

// Проверка на дублирование по IP-Адресу
//-------------------------------------------------------------------------------------------------
	Если (ЗначениеЗаполнено(ЭтотОбъект.IPАдрес)) Тогда
		ТекстЗапроса = "
		|SELECT	COUNT(*) AS Всего
		|FROM	Справочник.ТелСерверы
		|WHERE	(Ссылка <> &ЭтотОбъект) И (IPАдрес = &IPАдрес)";

		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("IPАдрес",ЭтотОбъект.IPАдрес);
		Запрос.УстановитьПараметр("ЭтотОбъект",ЭтотОбъект.Ссылка);
		Результат = Запрос.Выполнить().Выбрать();

		Если (Результат.Следующий()) И (Результат.Всего > 0) Тогда
			ТекстСообщения = "Сервер с IP-Адресом " + СокрЛП(ЭтотОбъект.IPАдрес) + " уже присутствует в справочнике";
			Сообщить(ТекстСообщения);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если (НЕ ЭтотОбъект.ПроверитьЗаполнение()) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры
