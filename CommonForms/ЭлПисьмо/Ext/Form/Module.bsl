
&НаКлиенте
Процедура Отправить(Команда)
	
	Если Не ЗначениеЗаполнено(ТелВнутреннийИсполнителя)Тогда
		ТелВнутр = "";
		Если ВвестиЗначение(ТелВнутр, "Номер внутреннего телефона получателя") Тогда
			ТелВнутреннийИсполнителя = ТелВнутр;
			Если ТелВнутреннийСуществует(ТелВнутреннийИсполнителя) Тогда
				ЭтаФорма.Кому = НомерВнутрТелефона_В_АдресЭлПочты(ЭтаФорма.ТелВнутреннийИсполнителя);	
			Иначе
				Сообщить("Внутренний номер телефона не существует. Закройте окно письма и откройте заново");
				ТелВнутреннийИсполнителя ="";
				Возврат
			КонецЕсли;
		Иначе
			Сообщить("Не введен номер внутреннего телефона получателя");
			Возврат
		КонецЕсли;
	КонецЕсли;
	
	//Удали из поля Кому записи, не являющиеся адресами эл. почты
	//-------------------------------------------------------------
	ВсеКому 	= СтрРазделить(ЭтаФорма.Кому,";");
	НовыйКому 	= "";
	Для Каждого Кому Из ВсеКому Цикл
		Если СтрНайти(Кому, "@") = 0 Тогда
			Продолжить;
		КонецЕсли;
		НовыйКому =НовыйКому + СокрЛП(Кому)+";";
	КонецЦикла;
	Если Прав(НовыйКому, 1)=";" Тогда
		НовыйКому=Лев(НовыйКому, СтрДлина(НовыйКому)-1);
	КонецЕсли;
	Кому = НовыйКому;
	
	#Область Закрытие_ПочтовойЗадачиГАЗ_Почтовым_сообщением_менеджеру	
	Доступно = 		ВладелецФормы <> Неопределено
				И 	ТипЗнч(ВладелецФормы)<>Тип("УправляемаяФорма")
				И 	ВладелецФормы.Имя="ЗадачиСписокГАЗ";
	Если Доступно Тогда
		От				= СокрЛП(От);
		Кому			= СокрЛП(Кому);
		Копия			= СокрЛП(Копия);
		СкрытаяКопия	= СокрЛП(СкрытаяКопия);
		Тема			= СокрЛП(Тема);
		Содержание		= СокрЛП(Содержание);
		
		//По умолчанию отправляем с авторизацией
		//--------------------------------------
		ПортSMTP = 587;	
		
		//Если отравляем только на луидоровские адреса, то без авторизации
		//-----------------------------------------------------------------
		Если СтрНайти(Кому,"@luidor")>0 И Не ЗначениеЗаполнено(Копия) Тогда
			ПортSMTP = 25;
		КонецЕсли;
		Если СтрНайти(Кому,"@luidor")>0 И ЗначениеЗаполнено(Копия) И СтрНайти(Копия,"@luidor")>0 Тогда
			ПортSMTP = 25;
		КонецЕсли;
		
		ЭлПочта.ПисьмоОтправить(От, Кому, Копия, СкрытаяКопия, Тема, Содержание,, ЭтаФорма.Параметры.ОтправлятьФоновымЗаданием, ПортSMTP);
		ЭтаФорма.Закрыть("ОтправленоПочтовоеСообщениеМенеджеруПоПочтовойЗадачеГАЗ");
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	Если Не ЗначениеЗаполнено(АбонентВнешний) Тогда  
		АбВнешн = "";
		Если ВвестиЗначение(АбВнешн, "Телефон абонента") Тогда   
			АбонентВнешний	= АбВнешн;
			Тема 		= ?(ЗначениеЗаполнено(Тема), 		Тема, 		"Перезвоните клиенту на номер: "+АбонентВнешний); 
			Содержание 	= ?(ЗначениеЗаполнено(Содержание), 	Содержание, "Перезвоните клиенту на номер: "+АбонентВнешний);
		Иначе
			Сообщить("Не введен номер телефона абонента");
			Возврат
		КонецЕсли;
	КонецЕсли;
	
	Если Не СоздатьЗадачу() Тогда
		Сообщить("Задача исполнителю не создана, Телефон абонента = "+АбонентВнешний);
		Возврат 	
	КонецЕсли;
	
	Если ОтправитьУведомлениеОЗадаче() Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры 


&НаСервере
Функция ТелВнутреннийСуществует(ТелВнутр) 
	Если Справочники.ТелВнутренние.НайтиПоКоду(ТелВнутр) = Неопределено Тогда
		Возврат Ложь
	Иначе
		Возврат Истина
	КонецЕсли;
КонецФункции

&НаСервере
Функция СоздатьЗадачу()
	
	//* ВАЖНО *
	//Отправка эл. письма находит существующую запись о задаче
	//и меняет в ней исполнителя с контакт-центра на того, кто указан в поле Кому письма (исполнитель определяется через телефон),
	// поэтому не нужно заменять этот код на РегистрыСведений.ЗадачиОбратныйЗвонок.Добавить()
	//Если запись не существует, будет создана новая запись
	
	Зап = РегистрыСведений.ЗадачиОбратныйЗвонок.СоздатьМенеджерЗаписи();  // это может быть или существующая или новая запись, нет проверки на выбра(), и это верно
	
	Зап.Телефон = СокрЛП(АбонентВнешний);
	Зап.Прочитать();
	
	Зап.ДатаАктуальности 	= ТекущаяДата() + 600*6;
	Зап.Тип 				= Перечисления.ТипЗадачи.Непринятый; 
	
	Если Не ЗначениеЗаполнено(Параметры.ТипЗадачи) Тогда		
		Зап.Тип = Перечисления.ТипЗадачи.Непринятый;
	Иначе 	
		Зап.Тип = ?(Параметры.ТипЗадачи = "Непринятый", Перечисления.ТипЗадачи.Непринятый,  Зап.Тип);  
		Зап.Тип = ?(Параметры.ТипЗадачи = "ПрочаяПочта",Перечисления.ТипЗадачи.ПрочаяПочта, Зап.Тип); 
	КонецЕсли;

	Зап.Комментарий = 	"Создана эл.письмом."															+ Символы.ПС +
						"От: "+ПараметрыСеанса.ТекущийПользователь										+ Символы.ПС +
						"Кому: "		+ Кому+ " (внутр. тел "+ЭтаФорма.ТелВнутреннийИсполнителя+")"	+ Символы.ПС +
						"Копия: "     	+ Копия															+ Символы.ПС +
						"Тема: "		+ Тема															+ Символы.ПС +
						"Содержание:" 	+ Содержание;  
	
	Зап.ДатаПостановки 		= ТекущаяДата();
	Зап.Исполнитель 		= Справочники.ТелВнутренние.НайтиПоКоду(ТелВнутреннийИсполнителя);
	Зап.ИсполнительПлан 	= Зап.Исполнитель;	
	Зап.СоИсполнительПлан 	= ?(ЗначениеЗаполнено(ТелВнутреннийСоисполнителя), Справочники.ТелВнутренние.НайтиПоКоду(ТелВнутреннийСоисполнителя), Зап.СоИсполнительПлан);	
	Зап.ЗвонокИнициатор 	= ?(Зап.Тип = Перечисления.ТипЗадачи.Непринятый, Параметры.Сигнатура, ""); 
	Зап.ПисьмоИнициатор 	= ?(Зап.Тип = Перечисления.ТипЗадачи.ПрочаяПочта, Параметры.Сигнатура, "");
	Зап.СоИсполнителиПлан 	= ПолучиПереченьСоисполнителей(ЭтаФорма.Копия);	
	Зап.ДатаЗакрытия 		= ТекущаяДата();  //Сделай эту задачу сразу закрытой

	Попытка
		Зап.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("СоздатьЗадачу()", УровеньЖурналаРегистрации.Ошибка, СтрШаблон("Телефон = %1, ДатаПостановки = %2", Зап.Телефон, Зап.ДатаПостановки), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
    Возврат Истина;
КонецФункции

&НаСервере
Функция ПолучиПереченьСоисполнителей(АдресаЭлПочты)
	Доступно = 	ТипЗнч(АдресаЭлПочты) = Тип("Строка") И СтрДлина(АдресаЭлПочты)>0;	
	Если Не Доступно Тогда
		Возврат "";
	КонецЕсли;
	
	ПереченьСоисполнителей = "";
	
	//Преобразуй в многострочный тескт
	//--------------------------------
	АдресаЭлПочтыМногострочно = СтрЗаменить(АдресаЭлПочты, ";", Символы.ПС);
	Для i = 1 По СтрЧислоСтрок(АдресаЭлПочтыМногострочно) Цикл
		
		//Получи наименование элемента предриятия ,содержащего этот адрес эл. почты
		ТекСтрока = СтрПолучитьСтроку(АдресаЭлПочтыМногострочно, i);
		ТекСтрока = СокрЛП(ТекСтрока);
		НаимЭлементаПредприятия = АдресЭлПочты_В_НаименованиеЭлементаПредприятия(ТекСтрока);
		Если НаимЭлементаПредприятия = "" Тогда
			Продолжить
		КонецЕсли;
		
		ПереченьСоисполнителей = ПереченьСоисполнителей + "; " + НаимЭлементаПредприятия;
	КонецЦикла;
	
	Если СтрНачинаетсяС(ПереченьСоисполнителей,";") Тогда
		ПереченьСоисполнителей = Прав(ПереченьСоисполнителей ,СтрДлина(ПереченьСоисполнителей)-1);
	КонецЕсли;
	
	ПереченьСоисполнителей = СокрЛП(ПереченьСоисполнителей);
	
	Если СтрЗаканчиваетсяНа(ПереченьСоисполнителей, ";") Тогда
		ПереченьСоисполнителей = Лев(ПереченьСоисполнителей, СтрДлина(ПереченьСоисполнителей)-1);
	КонецЕсли;
	
	Возврат ПереченьСоисполнителей;
КонецФункции

&НаСервере
Функция АдресЭлПочты_В_НаименованиеЭлементаПредприятия(АдресЭлПочты)
	
	Доступно = ТипЗнч(АдресЭлПочты) = Тип("Строка") И СтрДлина(АдресЭлПочты)>0;	
	Если Не Доступно Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 Наименование ИЗ Справочник.Предприятие ГДЕ АдресЭлектроннойПочты ПОДОБНО &АдресЭлПочты");
	Запрос.УстановитьПараметр("АдресЭлПочты", "%"+СокрЛП(АдресЭлПочты)+"%");	
	РЗ 			= Запрос.Выполнить();
	Результат 	= ?(РЗ.Пустой(), "", РЗ.Выгрузить()[0][0]);
	Возврат Результат	
КонецФункции

&НаСервере
Функция ОтправитьУведомлениеОЗадаче()
	
	ЗначениеПоУмолчанию = Ложь;
	
	ТекстСообщения = "";
	ТекстСообщения = ?(Не ЗначениеЗаполнено(От), 				"Не указан адрес отправителя. ", 		ТекстСообщения);
	ТекстСообщения = ?(Не ЗначениеЗаполнено(Кому), 				"Не указан адрес получателя. ", 		ТекстСообщения);
	ТекстСообщения = ?(Не ЗначениеЗаполнено(Тема), 				"Не указана тема. ", 					ТекстСообщения);
	ТекстСообщения = ?(Не ЗначениеЗаполнено(Содержание), 		"Не заполнено содержание", 				ТекстСообщения);	
	
	Если СтрДлина(ТекстСообщения)>0 Тогда
		Сообщить(ТекстСообщения);
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	От				= СокрЛП(От);
	Кому			= СокрЛП(Кому);
	Копия			= СокрЛП(Копия);
	СкрытаяКопия	= СокрЛП(СкрытаяКопия);
	Тема			= СокрЛП(Тема);
	Содержание		= СокрЛП(Содержание);
	
	//По умолчанию отправляем с авторизацией
	//--------------------------------------
	ПортSMTP = 587;	
	
	//Если отравляем только на луидоровские адреса, то без авторизации
	//-----------------------------------------------------------------
	Если СтрНайти(Кому,"@luidor")>0 И Не ЗначениеЗаполнено(Копия) Тогда
		ПортSMTP = 25;
	КонецЕсли;
	Если СтрНайти(Кому,"@luidor")>0 И ЗначениеЗаполнено(Копия) И СтрНайти(Копия,"@luidor")>0 Тогда
		ПортSMTP = 25;
	КонецЕсли;
	
	ЭлПочта.ПисьмоОтправить(От, Кому, Копия, СкрытаяКопия, Тема, Содержание,, ЭтаФорма.Параметры.ОтправлятьФоновымЗаданием, ПортSMTP);
	Возврат Истина;
КонецФункции

&НаСервере
Функция НомерВнутрТелефона_В_АдресЭлПочты(ТелВнутренний)
	
	Доступно = ТипЗнч(ТелВнутренний) = Тип("Строка") И ЗначениеЗаполнено(ТелВнутренний);	
	Если Не Доступно Тогда
		Возврат ""
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст ="ВЫБРАТЬ 	Т.Владелец.АдресЭлектроннойПочты КАК АдресЭлПочты
	              |ИЗ		РегистрСведений.ОбъектыПривязка КАК Т
	              |ГДЕ		ВЫРАЗИТЬ(Т.Объект КАК Справочник.ТелВнутренние).Код = &ТелВнутренний";
	Запрос.УстановитьПараметр("ТелВнутренний", ТелВнутренний);
	РЗ	= Запрос.Выполнить();  
	Рез = ?(РЗ.Пустой(), "", РЗ.Выгрузить()[0][0]);
	Возврат Рез;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АбонентВнешний = Параметры.АбонентВнешний;
	
	От = ?(ЗначениеЗаполнено(Параметры.От), Параметры.От, "info@luidor.ru"); 	
	
	ТелВнутреннийИсполнителя 	= Параметры.ТелВнутренний;	
	АдресЭлПочтыИсполнителя 	= НомерВнутрТелефона_В_АдресЭлПочты(ТелВнутреннийИсполнителя);	
	Кому 						= ?(ЗначениеЗаполнено(АдресЭлПочтыИсполнителя), АдресЭлПочтыИсполнителя, ""); 
	Если Не ЗначениеЗаполнено (АдресЭлПочтыИсполнителя) Тогда
		Сообщить("Не определен адрес эл. почты исполнителя в справочнике Предприятия. Заполните справочник...");
	КонецЕсли;
	
	Тема 		= ?(ЗначениеЗаполнено(Параметры.Тема), 			Параметры.Тема, 		"Перезвоните клиенту на номер: "+АбонентВнешний); 
	Содержание 	= ?(ЗначениеЗаполнено(Параметры.Содержание), 	Параметры.Содержание, 	"Перезвоните клиенту на номер: "+АбонентВнешний);

КонецПроцедуры

&НаКлиенте 
Процедура ОтНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФорму("Справочник.Предприятие.Форма.ФормаВыбора",,Элемент)
КонецПроцедуры

&НаКлиенте
Процедура ОтОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДобавьАдресЭлПочты(ЭтаФорма.От, ВыбранноеЗначение)
КонецПроцедуры

&НаКлиенте
Процедура КомуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФорму("Справочник.Предприятие.Форма.ФормаВыбора",,Элемент);
КонецПроцедуры

&НаСервере
Функция ПолучиВнутреннийТелефон(Сотрудник)	
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		Возврат ""
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ Объект.Код КАК НомерВнутреннегоТелефона 
	|ИЗ РегистрСведений.ОбъектыПривязка
	|ГДЕ Владелец = &Владелец И Объект ССЫЛКА Справочник.ТелВнутренние";	
	Запрос.УстановитьПараметр("Владелец", Сотрудник);
	РЗ = Запрос.Выполнить();   
	
	Результат = ?(РЗ.Пустой(), "", РЗ.Выгрузить()[0][0]);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура КомуОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Доступно = ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Предприятие") И ЗначениеЗаполнено(ВыбранноеЗначение);
	Если Не Доступно Тогда
		Возврат
	КонецЕсли;
	
	ТелВнутреннийИсполнителя =  ПолучиВнутреннийТелефон(ВыбранноеЗначение);
	ДобавьАдресЭлПочты(ЭтаФорма.Кому, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура КопияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФорму("Справочник.Предприятие.Форма.ФормаВыбора",,Элемент);
КонецПроцедуры  

&НаКлиенте
Процедура КопияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Доступно = ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Предприятие") И ЗначениеЗаполнено(ВыбранноеЗначение);
	Если Не Доступно Тогда
		Возврат
	КонецЕсли;

	ТелВнутреннийСоисполнителя	=  ПолучиВнутреннийТелефон(ВыбранноеЗначение);
	ДобавьАдресЭлПочты(ЭтаФорма.Копия, ВыбранноеЗначение)
КонецПроцедуры

&НаКлиенте
Процедура СкрытаяКопияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФорму("Справочник.Предприятие.Форма.ФормаВыбора",,Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СкрытаяКопияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Доступно = ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Предприятие") И ЗначениеЗаполнено(ВыбранноеЗначение);
	Если Не Доступно Тогда
		Возврат
	КонецЕсли;

	ДобавьАдресЭлПочты(ЭтаФорма.СкрытаяКопия, ВыбранноеЗначение)
КонецПроцедуры

&НаКлиенте
Процедура ДобавьАдресЭлПочты(СтрокаАдреса, ВыбранноеЗначение)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	АдресЭлПочты = ЭлементПредприятия_В_АдресЭлПочты(ВыбранноеЗначение);
	Если Не ЗначениеЗаполнено(АдресЭлПочты) Тогда
		Возврат;
	КонецЕсли;
	СтрокаАдреса = ?(ЗначениеЗаполнено(СтрокаАдреса), СтрокаАдреса+"; ", СтрокаАдреса);	
	СтрокаАдреса = СтрокаАдреса+АдресЭлПочты;	
КонецПроцедуры

&НаСервере
Функция ЭлементПредприятия_В_АдресЭлПочты(ПредприятиеСсылка)
	Возврат ПредприятиеСсылка.АдресЭлектроннойПочты	
КонецФункции

&НаКлиенте
Процедура КомуОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Кому = ?(СтрДлина(Текст) = 0, "", Кому);
КонецПроцедуры


