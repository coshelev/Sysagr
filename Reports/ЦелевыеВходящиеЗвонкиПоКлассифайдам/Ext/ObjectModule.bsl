
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Период 						=	ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")).Значение;

	ДатаНачала = Период.ДатаНачала;
	ДатаКонца  = Период.ДатаОкончания;
	
	ВсеКаналыИДаты = Новый ТаблицаЗначений();
	ВсеКаналыИДаты.Колонки.Добавить("Группа",	Новый ОписаниеТипов("Строка",,, Новый КвалификаторыСтроки(64)));
	ВсеКаналыИДаты.Колонки.Добавить("Канал", 	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(11)));
	ВсеКаналыИДаты.Колонки.Добавить("Дата", 	Новый ОписаниеТипов("Дата"));
	
	Для Каждого Стр Из ЭтотОбъект.Иерархия Цикл
		ТекДата = НачалоДня(ДатаНачала);
		Пока ТекДата <= НачалоДня(ДатаКонца) Цикл
			Новая = ВсеКаналыИДаты.Добавить();
			Новая.Группа 	= Стр.Группа; 
			Новая.Канал		= Стр.Сайт;
			Новая.Дата		= ТекДата;			
			ТекДата			= НачалоДня(ТекДата+(1*24*60*60));
		КонецЦикла;
		
	КонецЦикла;
		
	//Заполни ТЧ ЦелевыеЗвонкиОУ
	//---------------------------------------------------------------------
	Рез = СформироватьБазовуюТаблицуЦелевыеЗвонки(ДатаНачала, ДатаКонца);
	Если Рез = Ложь Тогда
		Сообщить("Ошибка заполнения таблицы ""Целевые звонки ОУ"" или ""Целевые звонки на основании интернет - заявок"" ");
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	Целевые.Сигнатура КАК Сигнатура
	|ПОМЕСТИТЬ ВТ05_ЦелевыеЗвонки
	|ИЗ
	|	&Целевые КАК Целевые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ05_ЦелевыеЗвонки.Сигнатура КАК Сигнатура,
	|	Звонки.Инициатор КАК Инициатор,
	|	Звонки.ЭтоВходящий КАК ЭтоВходящий,
	|	Звонки.АбонентВнешний КАК АбонентВнешний,
	|	Звонки.АбонентВнутренний КАК АбонентВнутренний,
	|	НАЧАЛОПЕРИОДА(Звонки.Дата, ДЕНЬ) КАК Дата,
	|	ДЕНЬНЕДЕЛИ(Звонки.Дата) КАК ДеньНедели,
	|	ТелВнешние.Код КАК НаименованиеКанала,
	|	ТелВнешние.Назначение КАК НазначениеКанала,
	|	ТелВнешние.Наименование + "" "" + ТелВнешние.Назначение КАК НаименованиеНазначениеКанала,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ВТ010_БезИерархии
	|ИЗ
	|	ВТ05_ЦелевыеЗвонки КАК ВТ05_ЦелевыеЗвонки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Звонки КАК Звонки
	|		ПО ВТ05_ЦелевыеЗвонки.Сигнатура = Звонки.Сигнатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТелВнешние КАК ТелВнешние
	|		ПО (Звонки.Инициатор = ТелВнешние.Ссылка)
	|ГДЕ
	|	Звонки.ЭтоВходящий = ИСТИНА
	|	И ТелВнешние.Ссылка В(&Каналы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЗ.Группа КАК Группа,
	|	ТЗ.Сайт КАК Сайт
	|ПОМЕСТИТЬ ВТ00_Иерархия
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ010_БезИерархии.Сигнатура КАК Сигнатура,
	|	ВТ010_БезИерархии.Инициатор КАК Инициатор,
	|	ВТ010_БезИерархии.Дата КАК Дата,
	|	ВТ010_БезИерархии.ДеньНедели КАК ДеньНедели,
	|	ВТ010_БезИерархии.НаименованиеНазначениеКанала КАК НаименованиеНазначениеКанала,
	|	ВТ010_БезИерархии.Количество КАК Количество,
	|	ВТ00_Иерархия.Группа КАК Группа
	|ПОМЕСТИТЬ ВТ020_РезультатБезДополнения
	|ИЗ
	|	ВТ010_БезИерархии КАК ВТ010_БезИерархии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ00_Иерархия КАК ВТ00_Иерархия
	|		ПО ВТ010_БезИерархии.НаименованиеКанала = ВТ00_Иерархия.Сайт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Все.Группа КАК Группа,
	|	Все.Канал КАК Канал,
	|	Все.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ030_Дополнение
	|ИЗ
	|	&ВсеКаналыИДаты КАК Все
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ030.Группа КАК Группа,
	|	ВТ030.Канал КАК Канал,
	|	ТелВнешние.Наименование + "" "" + ТелВнешние.Назначение КАК НаименованиеНазначениеКанала,
	|	ВТ030.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ035_Дополнение
	|ИЗ
	|	ВТ030_Дополнение КАК ВТ030
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТелВнешние КАК ТелВнешние
	|		ПО ВТ030.Канал = ТелВнешние.Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ020.Сигнатура КАК Сигнатура,
	|	ВТ020.Инициатор КАК Инициатор,
	|	ВТ020.Дата КАК Дата,
	|	ВТ020.ДеньНедели КАК ДеньНедели,
	|	ВТ020.НаименованиеНазначениеКанала КАК НаименованиеНазначениеКанала,
	|	ВТ020.Количество КАК Количество,
	|	ВТ020.Группа КАК Группа
	|ИЗ
	|	ВТ020_РезультатБезДополнения КАК ВТ020
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	"""",
	|	"""",
	|	ВТ035.Дата,
	|	ДЕНЬНЕДЕЛИ(ВТ035.Дата),
	|	ВТ035.НаименованиеНазначениеКанала,
	|	0,
	|	ВТ035.Группа
	|ИЗ
	|	ВТ035_Дополнение КАК ВТ035";
	
	Запрос.УстановитьПараметр("Целевые", 		ЭтотОбъект.ЦелевыеЗвонки);
	Запрос.УстановитьПараметр("Каналы",			ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Каналы")).Значение.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ТЗ", 			ЭтотОбъект.Иерархия);
	Запрос.УстановитьПараметр("ВсеКаналыИДаты",	ВсеКаналыИДаты);

	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	ТЗ = РезультатЗапроса.Выгрузить();

	ВнешниеНаборыДанных = Новый Структура();
	ВнешниеНаборыДанных.Вставить("ТЗ", ТЗ);
	
	СКД = ПолучитьМакет("МакетНаТЗ");
	Настройки = СКД.НастройкиПоУмолчанию;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, Настройки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ПоказатьУровеньГруппировокСтрок(1);

 КонецПроцедуры
 
Процедура ЗаписатьИерархию() Экспорт
	

	Если ЭтотОбъект.Иерархия.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим, что один и тот же сайт не встречается более одного раза
	//------------------------------------------------------------------
	Запрос = Новый  Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЗ.Сайт КАК Сайт
	|ПОМЕСТИТЬ ВТ01
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ01.Сайт КАК Сайт,
	|	СУММА(1) КАК Поле1
	|ИЗ
	|	ВТ01 КАК ВТ01
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ01.Сайт
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1";
	Запрос.УстановитьПараметр("ТЗ", ЭтотОбъект.Иерархия);
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если Не РезультатЗапроса.Пустой() Тогда
		Сообщить("Один и тот же номер повторяется в разных группах. Иерархия не сохранена.");
		Возврат
	КонецЕсли;
	
	
	Зап = Новый ЗаписьXML();
	Зап.УстановитьСтроку();
	Зап.ЗаписатьОбъявлениеXML();
	Зап.ЗаписатьНачалоЭлемента("root"); 	// <root>

	Для Каждого СтрТЧ Из ЭтотОбъект.Иерархия Цикл
		Зап.ЗаписатьНачалоЭлемента("row");  	//<row>

			Зап.ЗаписатьНачалоЭлемента("group");	//<group>
			Зап.ЗаписатьТекст(СтрТЧ.Группа);
			Зап.ЗаписатьКонецЭлемента(); 			//</group>

			Зап.ЗаписатьНачалоЭлемента("site");		//<site>
			Зап.ЗаписатьТекст(СтрТЧ.Сайт);
			Зап.ЗаписатьКонецЭлемента(); 			//</site>

	    Зап.ЗаписатьКонецЭлемента(); 			//</row>

	КонецЦикла;
	
	Зап.ЗаписатьКонецЭлемента(); 			 //	</root>

	Стр=Зап.Закрыть();
	
	Если ЭтотОбъект.ИерархияСтрокой = Стр Тогда
		Возврат
	КонецЕсли;
	
	ТекДата = ТекущаяДата();
	НЗ = РегистрыСведений.УчетнаяПолитика.СоздатьНаборЗаписей();
	НЗ.Отбор.Период.Установить(ТекДата);
	НЗ.Отбор.Параметр.Установить("Отчет_ЦелевыеВходящиеЗвонкиПоКлассифайдам_Иерархия");
	НЗ.Прочитать();
	
	Новая = НЗ.Добавить();
	Новая.Период 			= ТекДата;
	Новая.Параметр 			= "Отчет_ЦелевыеВходящиеЗвонкиПоКлассифайдам_Иерархия";
	Новая.ТипЗначения 			= "Строка";
	Новая.Значение 			= Стр;
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		НЗ.Записать();
	Исключение
		Данные = "Стр = "+Стр;
		Комментарий = ОписаниеОшибки();
		ОбслуживаниеСервер.ЗарегистрироватьСобытие("Записать параметр учетной политики", УровеньЖурналаРегистрации.Ошибка, Данные, Комментарий,,,,ИмяКомпьютера());
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция СформироватьБазовуюТаблицуЦелевыеЗвонки(ДатаНачала, ДатаОкончания)
	
	ЭтотОбъект.ЦелевыеЗвонки.Очистить();
		
	ЗначениеПоУмолчанию = Ложь;
		
	//Прочитай данные о сделках из ОУ
	//-------------------------------
	ИсточникДанных = Справочники.ИБ_ИсточникиДанных.НайтиПоКоду("000000002");
	Если ИсточникДанных = Неопределено Тогда
		Сообщить("Нет подключения к базе оперативного учета");
		Возврат ЗначениеПоУмолчанию
	Иначе
		ПараметрыСоединения = ИсточникДанных.ПараметрыСоединения;
		БИ = ОбщегоНазначения.ПолучитьПодключениеБД(ПараметрыСоединения);	
	КонецЕсли;

	БИ_Запрос = БИ.NewObject("Запрос");
	БИ_Запрос.Текст = 
	"ВЫБРАТЬ Т.Код КАК Сигнатура, ПРЕДСТАВЛЕНИЕ(Т.Состояние) КАК Статус
	|ИЗ Справочник.Звонки КАК Т
	|ГДЕ Т.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)";
	
	БИ_Запрос.УстановитьПараметр("ДатаНачала",				ДатаНачала);
	БИ_Запрос.УстановитьПараметр("ДатаОкончания", 			ДатаОкончания);	
	
	БИ_РезультатЗапроса = БИ_Запрос.Выполнить();	
	БИ_Выборка = БИ_Запрос.Выполнить().Выбрать();
	
	Пока БИ_Выборка.Следующий() Цикл
		Новая = ЭтотОбъект["ЦелевыеЗвонки"].Добавить();	
		Новая.Сигнатура = БИ_Выборка.Сигнатура;
		Новая.Статус	= БИ_Выборка.Статус;
	КонецЦикла;
	
	Возврат Истина
	
КонецФункции


