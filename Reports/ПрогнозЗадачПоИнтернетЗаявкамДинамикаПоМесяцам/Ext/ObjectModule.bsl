
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Период 						=	ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")).Значение;

	ДатаНачала = Период.ДатаНачала;
	ДатаКонца  = Период.ДатаОкончания;
	
	//Заполни ТЧ ЗадачиИнтенетЗаявки
	//---------------------------------
	Рез = СформироватьБазовуюТаблицуЗадачиИнтернетЗаявки(ДатаНачала, ДатаКонца);
	Если Рез = Ложь Тогда
		Сообщить("Ошибка заполнения таблицы Задачи по интернет-заявкам");
		Возврат
	КонецЕсли;
	
	//Заполни ТЧ ЦелевыеЗвонкиОУ и ТЧ елевыеЗвонкиНаОснованииИнтернетЗаявок
	//---------------------------------------------------------------------
	Рез = СформироватьБазовуюТаблицуЦелевыеЗвонкиНаОснованииИнтернетЗаявок(ДатаНачала, ДатаКонца);
	Если Рез = Ложь Тогда
		Сообщить("Ошибка заполнения таблицы ""Целевые звонки ОУ"" или ""Целевые звонки на основании интернет - заявок"" ");
		Возврат
	КонецЕсли;

	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Свойства.Значение.Наименование КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ003_ВсеСвойства
	|ИЗ
	|	РегистрСведений.ИнтернетЗаявкиСвойства КАК Свойства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиИнтернетЗаявки КАК Задачи
	|		ПО Свойства.Сигнатура = Задачи.Сигнатура
	|ГДЕ
	|	Свойства.Свойство.Наименование = &ИмяСвойства
	|	И Задачи.ДатаПостановки МЕЖДУ &ДатаНачала И &ДатаКонца
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	""<источник заявки не определен>""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ003.ИсточникЗаявки КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ004_ОдноЛюбоеСвойствоДляПрикрепленияСтоимости
	|ИЗ
	|	ВТ003_ВсеСвойства КАК ВТ003
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиИнтернетЗаявки.Сигнатура КАК Сигнатура,
	|	ЗадачиИнтернетЗаявки.Телефон КАК Телефон,
	|	ЗадачиИнтернетЗаявки.Дата КАК Дата,
	|	ЗадачиИнтернетЗаявки.Инициатор КАК Инициатор,
	|	ЗадачиИнтернетЗаявки.ИсточникЗаявки КАК ИсточникЗаявки,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ВТ0041_ЗадачиИнтернетЗаявки
	|ИЗ
	|	&ЗадачиИнтернетЗаявки КАК ЗадачиИнтернетЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦелевыеНаОсновании.СигнатураЗаявки КАК СигнатураЗаявки,
	|	1 КАК КоличествоЦелевыхЗаявок
	|ПОМЕСТИТЬ ВТ0042_ЦелевыеЗаявки
	|ИЗ
	|	&ЦелевыеЗвокиНаОснованииИнтернетЗаявок КАК ЦелевыеНаОсновании
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ0041.Сигнатура КАК Сигнатура,
	|	ВТ0041.Дата КАК Дата,
	|	ВТ0041.Инициатор КАК Инициатор,
	|	ВТ0041.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ВТ0041.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ВТ0041.Телефон ПОДОБНО &Тел0000000000
	|			ТОГДА 1
	|		КОГДА ВТ0041.Сигнатура В (&ЗаявкиОснованияВходящихЗвонковКорпоративногоОтдела)
	|			ТОГДА 1
	|		КОГДА ВТ0041.Сигнатура В (&ПредполагаемыеЗаявкиЦелевыхБезЗаявок)
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ВТ0042.КоличествоЦелевыхЗаявок, 0)
	|	КОНЕЦ КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ0043_ЗадачиИнтернетЗаявки
	|ИЗ
	|	ВТ0041_ЗадачиИнтернетЗаявки КАК ВТ0041
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ0042_ЦелевыеЗаявки КАК ВТ0042
	|		ПО ВТ0041.Сигнатура = ВТ0042.СигнатураЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Задачи.Дата, МЕСЯЦ) КАК Дата,
	|	Задачи.Инициатор КАК Инициатор,
	|	Задачи.ИсточникЗаявки КАК ИсточникЗаявки,
	|	СУММА(Задачи.Количество) КАК Количество,
	|	СУММА(Задачи.КоличествоЦелевых) КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ02_ТекПериод
	|ИЗ
	|	ВТ0043_ЗадачиИнтернетЗаявки КАК Задачи
	|
	|СГРУППИРОВАТЬ ПО
	|	Задачи.Инициатор,
	|	НАЧАЛОПЕРИОДА(Задачи.Дата, МЕСЯЦ),
	|	Задачи.ИсточникЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ02.Дата КАК Дата,
	|	ВТ02.Инициатор КАК Инициатор,
	|	ВТ003.ИсточникЗаявки КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ021_ВсеСочетанияСайтИсточникТек
	|ИЗ
	|	ВТ02_ТекПериод КАК ВТ02
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ003_ВсеСвойства КАК ВТ003
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ21.Дата КАК Дата,
	|	ВТ21.Инициатор КАК Инициатор,
	|	ВТ21.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ЕСТЬNULL(ВТ02.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТ02.КоличествоЦелевых, 0) КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ022_ТекПериод
	|ИЗ
	|	ВТ021_ВсеСочетанияСайтИсточникТек КАК ВТ21
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ02_ТекПериод КАК ВТ02
	|		ПО ВТ21.Дата = ВТ02.Дата
	|			И ВТ21.Инициатор = ВТ02.Инициатор
	|			И ВТ21.ИсточникЗаявки = ВТ02.ИсточникЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ02.Дата КАК ДатаТек,
	|	ВТ02.Инициатор КАК Инициатор,
	|	ВТ02.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ВТ02.Количество КАК Количество,
	|	ВТ02.КоличествоЦелевых КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ04
	|ИЗ
	|	ВТ022_ТекПериод КАК ВТ02
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СайтыКомпании.Ссылка.Наименование, ""<не найден сайт для компании>"") КАК Сайт,
	|	ВЫБОР
	|		КОГДА Рег.Сервис.Наименование = ""Google AdWords""
	|			ТОГДА ""Контекстная реклама Google""
	|		КОГДА Рег.Сервис.Наименование = ""direct.yandex.ru""
	|			ТОГДА ""Контекстная реклама Яндекс""
	|		ИНАЧЕ ""<источник заявки не определен>""
	|	КОНЕЦ КАК ИсточникЗаявки,
	|	НАЧАЛОПЕРИОДА(Рег.ПериодЗаявки, МЕСЯЦ) КАК Период,
	|	СУММА(ВЫБОР
	|			КОГДА Рег.Сервис.Наименование = ""Google AdWords""
	|				ТОГДА Рег.Стоимость * &Курс * 1.2
	|			ИНАЧЕ Рег.Стоимость
	|		КОНЕЦ) КАК Стоимость
	|ПОМЕСТИТЬ ВТ0011_СтоимостиЗаявокПоСайтамПоПериодам
	|ИЗ
	|	РегистрСведений.ИнтернетЗаявкиСтоимость КАК Рег
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сайты.Компании КАК СайтыКомпании
	|		ПО Рег.ИдКомпании = СайтыКомпании.Ид
	|ГДЕ
	|	Рег.ПериодЗаявки МЕЖДУ &ДатаНачала И &ДатаКонца
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(СайтыКомпании.Ссылка.Наименование, ""<не найден сайт для компании>""),
	|	ВЫБОР
	|		КОГДА Рег.Сервис.Наименование = ""Google AdWords""
	|			ТОГДА ""Контекстная реклама Google""
	|		КОГДА Рег.Сервис.Наименование = ""direct.yandex.ru""
	|			ТОГДА ""Контекстная реклама Яндекс""
	|		ИНАЧЕ ""<источник заявки не определен>""
	|	КОНЕЦ,
	|	НАЧАЛОПЕРИОДА(Рег.ПериодЗаявки, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ04.ДатаТек, ВТ0011.Период) КАК ДатаТек,
	|	ЕСТЬNULL(ВТ04.Инициатор, ВТ0011.Сайт) КАК Инициатор,
	|	ЕСТЬNULL(ВТ04.ИсточникЗаявки, ВТ0011.ИсточникЗаявки) КАК ИсточникЗаявки,
	|	ЕСТЬNULL(ВТ04.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТ04.КоличествоЦелевых, 0) КАК КоличествоЦелевых,
	|	ЕСТЬNULL(ВТ0011.Стоимость, 0) КАК СтоимостьКомпанийСайта,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ0011.Стоимость, 0) > 0
	|			ТОГДА ЕСТЬNULL(ВТ04.Количество, 0) / ЕСТЬNULL(ВТ0011.Стоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьЗаявки
	|ПОМЕСТИТЬ ВТ05
	|ИЗ
	|	ВТ04 КАК ВТ04
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ0011_СтоимостиЗаявокПоСайтамПоПериодам КАК ВТ0011
	|		ПО ВТ04.Инициатор = ВТ0011.Сайт
	|			И ВТ04.ИсточникЗаявки = ВТ0011.ИсточникЗаявки
	|			И ВТ04.ДатаТек = ВТ0011.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЗ.Группа КАК Группа,
	|	ТЗ.Сайт КАК Сайт
	|ПОМЕСТИТЬ ВТ00_Иерархия
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ05.ДатаТек КАК ДатаТек,
	|	ВТ05.Инициатор КАК Инициатор,
	|	ВТ05.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ВТ05.Количество КАК Количество,
	|	ВТ05.КоличествоЦелевых КАК КоличествоЦелевых,
	|	ЕСТЬNULL(ВТ00.Группа, ""Прочие"") КАК Группа,
	|	ВТ05.СтоимостьКомпанийСайта КАК СтоимостьКомпанийСайта,
	|	ВТ05.СтоимостьЗаявки КАК СтоимостьЗаявки
	|ИЗ
	|	ВТ05 КАК ВТ05
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ00_Иерархия КАК ВТ00
	|		ПО ВТ05.Инициатор = ВТ00.Сайт" ;
	
	Период 		=  ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")).Значение;
	Запрос.УстановитьПараметр("ДатаНачала", 	Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца", 		Период.ДатаОкончания);
	
	Запрос.УстановитьПараметр("ИмяСвойства",	"referrer_source");
	Запрос.УстановитьПараметр("Курс",   ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Курс")).Значение);
	
	Запрос.УстановитьПараметр("ТЗ", ЭтотОбъект.Иерархия);
	
	Запрос.УстановитьПараметр("ЗадачиИнтернетЗаявки", ЭтотОбъект.ЗадачиИнтернетЗаявки);
	Запрос.УстановитьПараметр("ЦелевыеЗвокиНаОснованииИнтернетЗаявок", ЭтотОбъект.ЦелевыеЗвокиНаОснованииИнтернетЗаявок);
	
	Запрос.УстановитьПараметр("Тел0000000000", "0000000000%");
	
	Запрос.УстановитьПараметр("ЗаявкиОснованияВходящихЗвонковКорпоративногоОтдела", ЭтотОбъект.ВходящиеЗвонкиКорпоративногоОтдела.ВыгрузитьКолонку("СигнатураЗаявки"));
	
	Запрос.УстановитьПараметр("ПредполагаемыеЗаявкиЦелевыхБезЗаявок", ЭтотОбъект.ПредполагаемыеЗаявкиЦелевыхБезЗаявок.ВыгрузитьКолонку("СигнатураЗаявки"));
	
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	ТЗ = РезультатЗапроса.Выгрузить();

	ВнешниеНаборыДанных = Новый Структура();
	ВнешниеНаборыДанных.Вставить("ТЗ", ТЗ);
	
	СКД = ПолучитьМакет("МакетНаТЗ");
	Настройки = СКД.НастройкиПоУмолчанию;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, Настройки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ПоказатьУровеньГруппировокСтрок(1);

 КонецПроцедуры
 
Процедура ЗаписатьИерархию() Экспорт
	

	Если ЭтотОбъект.Иерархия.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим, что один и тот же сайт не встречается более одного раза
	//------------------------------------------------------------------
	Запрос = Новый  Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЗ.Сайт КАК Сайт
	|ПОМЕСТИТЬ ВТ01
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ01.Сайт КАК Сайт,
	|	СУММА(1) КАК Поле1
	|ИЗ
	|	ВТ01 КАК ВТ01
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ01.Сайт
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1";
	Запрос.УстановитьПараметр("ТЗ", ЭтотОбъект.Иерархия);
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если Не РезультатЗапроса.Пустой() Тогда
		Сообщить("Один и тот же сайт повторяется в разных группах. Иерархия не сохранена.");
		Возврат
	КонецЕсли;
	
	
	Зап = Новый ЗаписьXML();
	Зап.УстановитьСтроку();
	Зап.ЗаписатьОбъявлениеXML();
	Зап.ЗаписатьНачалоЭлемента("root"); 	// <root>

	Для Каждого СтрТЧ Из ЭтотОбъект.Иерархия Цикл
		Зап.ЗаписатьНачалоЭлемента("row");  	//<row>

			Зап.ЗаписатьНачалоЭлемента("group");	//<group>
			Зап.ЗаписатьТекст(СтрТЧ.Группа);
			Зап.ЗаписатьКонецЭлемента(); 			//</group>

			Зап.ЗаписатьНачалоЭлемента("site");		//<site>
			Зап.ЗаписатьТекст(СтрТЧ.Сайт);
			Зап.ЗаписатьКонецЭлемента(); 			//</site>

	    Зап.ЗаписатьКонецЭлемента(); 			//</row>

	КонецЦикла;
	
	Зап.ЗаписатьКонецЭлемента(); 			 //	</root>

	Стр=Зап.Закрыть();
	
	Если ЭтотОбъект.ИерархияСтрокой = Стр Тогда
		Возврат
	КонецЕсли;
	
	ТекДата = ТекущаяДата();
	НЗ = РегистрыСведений.УчетнаяПолитика.СоздатьНаборЗаписей();
	НЗ.Отбор.Период.Установить(ТекДата);
	НЗ.Отбор.Параметр.Установить("Отчет_ПрогнозЗадачПоИнтернетЗаявкам_Иерархия");
	НЗ.Прочитать();
	
	Новая = НЗ.Добавить();
	Новая.Период 			= ТекДата;
	Новая.Параметр 			= "Отчет_ПрогнозЗадачПоИнтернетЗаявкам_Иерархия";
	Новая.ТипЗначения 			= "Строка";
	Новая.Значение 			= Стр;
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		НЗ.Записать();
	Исключение
		Данные = "Стр = "+Стр;
		Комментарий = ОписаниеОшибки();
		ОбслуживаниеСервер.ЗарегистрироватьСобытие("Записать параметр учетной политики", УровеньЖурналаРегистрации.Ошибка, Данные, Комментарий,,,,ИмяКомпьютера());
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция СформироватьБазовуюТаблицуЗадачиИнтернетЗаявки (ДатаНачала, ДатаОкончания)    
	
	ЭтотОбъект.ЗадачиИнтернетЗаявки.Очистить();
	ЗначениеПоУмолчанию = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Заявки.Сигнатура КАК Сигнатура,
	|	Заявки.АбонентВнешний КАК Телефон,
	|	Заявки.Дата КАК Дата,
	|	Задачи.Инициатор КАК Инициатор,
	|	ЕСТЬNULL(Свойства.Значение.Наименование, &ПустаяСтрока) КАК ИсточникЗаявки,
	|	1 КАК Количество
	|ИЗ
	|	РегистрСведений.ЗадачиИнтернетЗаявки КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИнтернетЗаявки КАК Заявки
	|		ПО Задачи.Сигнатура = Заявки.Сигнатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнтернетЗаявкиСвойства КАК Свойства
	|		ПО (Заявки.Сигнатура = Свойства.Сигнатура)
	|			И (Свойства.Свойство.Наименование = &ИмяСвойства)
	|ГДЕ
	|	Задачи.ДатаПостановки МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)";
	
	Запрос.УстановитьПараметр("ДатаНачала", 	ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ДатаОкончания);
	Запрос.УстановитьПараметр("ИмяСвойства",	"referrer_source");
	Запрос.УстановитьПараметр("ПустаяСтрока",	"<источник заявки не определен>");
		
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Новая = ЭтотОбъект.ЗадачиИнтернетЗаявки.Добавить();
		Новая.Сигнатура 		= Выборка.Сигнатура;
		Новая.Телефон			= Выборка.Телефон;
		Новая.Дата				= Выборка.Дата;
		Новая.Инициатор			= Выборка.Инициатор;
		Новая.ИсточникЗаявки	= Выборка.ИсточникЗаявки
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

Функция СформироватьБазовуюТаблицуЦелевыеЗвонкиНаОснованииИнтернетЗаявок(ДатаНачала, ДатаОкончания)
	
	// В это отчете не учитываем
	//-----------------------------------
	Возврат Истина;
	
	ЭтотОбъект.ЦелевыеЗвонки.Очистить();
	ЭтотОбъект.ЦелевыеЗвокиНаОснованииИнтернетЗаявок.Очистить();
		
	ЗначениеПоУмолчанию = Ложь;
		
	//Прочитай данные о сделках из ОУ
	//-------------------------------
	ИсточникДанных = Справочники.ИБ_ИсточникиДанных.НайтиПоКоду("000000002");
	Если ИсточникДанных = Неопределено Тогда
		Сообщить("Нет подключения к базе оперативного учета");
		Возврат ЗначениеПоУмолчанию
	Иначе
		ПараметрыСоединения = ИсточникДанных.ПараметрыСоединения;
		БИ = ОбщегоНазначения.ПолучитьПодключениеБД(ПараметрыСоединения);	
	КонецЕсли;

	БИ_Запрос = БИ.NewObject("Запрос");
	БИ_Запрос.Текст = 
	
	"ВЫБРАТЬ
	|ЗвонкиЦелевые.Сигнатура КАК Сигнатура,
	|ПРЕДСТАВЛЕНИЕ(ЗвонкиЦелевые.Статус) КАК Статус
	|ИЗ
	|РегистрСведений.ЗвонкиЦелевые КАК ЗвонкиЦелевые
	|ГДЕ
	|ЗвонкиЦелевые.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаОкончания, ДЕНЬ, 10), ДЕНЬ)
	|И ЗвонкиЦелевые.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусЦелевогоЗвонка.Отложен), ЗНАЧЕНИЕ(Перечисление.СтатусЦелевогоЗвонка.Ожидание), ЗНАЧЕНИЕ(Перечисление.СтатусЦелевогоЗвонка.ОтказПередумал), ЗНАЧЕНИЕ(Перечисление.СтатусЦелевогоЗвонка.Сделка), ЗНАЧЕНИЕ(Перечисление.СтатусЦелевогоЗвонка.ОтказКорпоративный))";
	
	БИ_Запрос.УстановитьПараметр("ДатаНачала",				ДатаНачала);
	БИ_Запрос.УстановитьПараметр("ДатаОкончания", 			ДатаОкончания);	
	
	БИ_РезультатЗапроса = БИ_Запрос.Выполнить();	
	БИ_Выборка = БИ_Запрос.Выполнить().Выбрать();
	
	Пока БИ_Выборка.Следующий() Цикл
		Новая = ЭтотОбъект["ЦелевыеЗвонки"].Добавить();	
		Новая.Сигнатура = БИ_Выборка.Сигнатура;
		Новая.Статус	= БИ_Выборка.Статус;
	КонецЦикла;

	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.Сигнатура КАК Сигнатура
	|ПОМЕСТИТЬ ВТ00_ЦелевыеЗвонки
	|ИЗ
	|	&РеестрЦелевых КАК ТЧ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ00.Сигнатура КАК Сигнатура,
	|	ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 4, 36) КАК Основание
	|ПОМЕСТИТЬ ВТ01_ЦелевыеНаОснованииИнтернетЗаявок
	|ИЗ
	|	РегистрСведений.ЗвонкиСтатОбщая КАК ЗвонкиСтатОбщая
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ00_ЦелевыеЗвонки КАК ВТ00
	|		ПО ЗвонкиСтатОбщая.Сигнатура = ВТ00.Сигнатура
	|ГДЕ
	|	ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 1, 2) = &ПрефиксСигнатурыИнтернетЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиИнтернетЗаявки.Сигнатура КАК СигнатураЗаявки
	|ПОМЕСТИТЬ ВТ02_ЗадачиИнтернетЗаявки
	|ИЗ
	|	&ЗадачиИнтернетЗаявки КАК ЗадачиИнтернетЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ01.Сигнатура КАК СигнатураЗвонка,
	|	ВТ02.СигнатураЗаявки КАК СигнатураЗаявки
	|ИЗ
	|	ВТ01_ЦелевыеНаОснованииИнтернетЗаявок КАК ВТ01
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ02_ЗадачиИнтернетЗаявки КАК ВТ02
	|		ПО ВТ01.Основание = ВТ02.СигнатураЗаявки";
	
	Запрос.УстановитьПараметр("РеестрЦелевых", ЭтотОбъект.ЦелевыеЗвонки);
	Запрос.УстановитьПараметр("ЗадачиИнтернетЗаявки", ЭтотОбъект.ЗадачиИнтернетЗаявки);
	Запрос.УстановитьПараметр("ПрефиксСигнатурыИнтернетЗаявки", "iq");
	
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ЗначениеПоУмолчанию
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Новая = ЭтотОбъект.ЦелевыеЗвокиНаОснованииИнтернетЗаявок.Добавить();
		Новая.СигнатураЗвонка 	= Выборка.СигнатураЗвонка;
		Новая.СигнатураЗаявки	= Выборка.СигнатураЗаявки;
	КонецЦикла;
	
	Возврат Истина
	
КонецФункции

