
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиЗвонокЛояльности.СигнатураПродажи КАК СигнатураЗадачи,
	|	ЗадачиЗвонокЛояльности.Телефон КАК Телефон,
	|	Продажи.Точка КАК Точка,
	|	Продажи.Номер КАК Номер,
	|	Продажи.Дата КАК Дата,
	|	Продажи.КонтактноеЛицо КАК КонтактноеЛицо,
	|	Продажи.Сотрудник КАК Сотрудник,
	|	Продажи.Объект КАК Автомобиль,
	|	ЗадачиЗвонокЛояльности.ДатаЗакрытия КАК ДатаЗакрытия
	|ПОМЕСТИТЬ ВТ01_Задачи
	|ИЗ
	|	РегистрСведений.ЗадачиЗвонокЛояльности КАК ЗадачиЗвонокЛояльности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Продажи КАК Продажи
	|		ПО ЗадачиЗвонокЛояльности.СигнатураПродажи = Продажи.Сигнатура
	|			И (Продажи.Объект ПОДОБНО ""%Vin: Z783%"")
	|ГДЕ
	|	ЗадачиЗвонокЛояльности.ДатаЗакрытия МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачалаПериодаЗакрытияЗадач, ДЕНЬ) И КОНЕЦПЕРИОДА(&ДатаКонцаПериодаЗакрытияЗадач, ДЕНЬ)
	|	И ЗадачиЗвонокЛояльности.Тип = ЗНАЧЕНИЕ(Перечисление.ТипЗадачи.ЛояльностьПродажАвто)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АнкетыРасширенные.СигнатураЗадачи КАК СигнатураЗадачи,
	|	АнкетыРасширенные.Вопрос КАК Вопрос,
	|	АнкетыРасширенные.Ответ КАК Ответ,
	|	АнкетыРасширенные.ОтветКомментарий.ПолнаяСтрока КАК ОтветКомментарий,
	|	ВТ01.Телефон КАК Телефон,
	|	ВТ01.Точка КАК Точка,
	|	ВТ01.Номер КАК Номер,
	|	ВТ01.Дата КАК Дата,
	|	ВТ01.КонтактноеЛицо КАК КонтактноеЛицо,
	|	ВТ01.Сотрудник КАК Сотрудник,
	|	ВТ01.Автомобиль КАК Автомобиль,
	|	ВТ01.ДатаЗакрытия КАК ДатаЗакрытия
	|ПОМЕСТИТЬ ВТ02_АнкетыЗадачБезФильтраПоВопросам
	|ИЗ
	|	РегистрСведений.АнкетыРасширенные КАК АнкетыРасширенные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ01_Задачи КАК ВТ01
	|		ПО (ВТ01.СигнатураЗадачи = АнкетыРасширенные.СигнатураЗадачи)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ02_АнкетыЗадачБезФильтраПоВопросам.СигнатураЗадачи КАК СигнатураЗадачи,
	|	ВТ02_АнкетыЗадачБезФильтраПоВопросам.Вопрос КАК Вопрос,
	|	ВТ02_АнкетыЗадачБезФильтраПоВопросам.ДатаЗакрытия КАК ДатаЗакрытия
	|ПОМЕСТИТЬ ВТ03_ОтборПоВопросам
	|ИЗ
	|	ВТ02_АнкетыЗадачБезФильтраПоВопросам КАК ВТ02_АнкетыЗадачБезФильтраПоВопросам
	|ГДЕ
	|	ВТ02_АнкетыЗадачБезФильтраПоВопросам.Вопрос = &Вопрос1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ02.Точка КАК Точка,
	|	ВТ02.Номер КАК Номер,
	|	ВТ02.Дата КАК Дата,
	|	ВТ02.Сотрудник КАК Сотрудник,
	|	ВТ02.Автомобиль КАК Автомобиль,
	|	ВТ02.Телефон КАК Телефон,
	|	ВТ02.КонтактноеЛицо КАК КонтактноеЛицо,
	|	ВТ02.СигнатураЗадачи КАК СигнатураЗадачи,
	|	ВТ02.Вопрос КАК Вопрос,
	|	ВТ02.Ответ КАК Ответ,
	|	ВТ02.ОтветКомментарий КАК ОтветКомментарий,
	|	ВТ02.ДатаЗакрытия КАК ДатаЗакрытия,
	|	""pr_"" + ВТ02.СигнатураЗадачи КАК ОписаниеЗадачиКакОснованиеЗвонка
	|ПОМЕСТИТЬ ВТ04_БезЗвонков
	|ИЗ
	|	ВТ02_АнкетыЗадачБезФильтраПоВопросам КАК ВТ02
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ03_ОтборПоВопросам КАК ВТ03_Отбор
	|		ПО ВТ02.СигнатураЗадачи = ВТ03_Отбор.СигнатураЗадачи
	|			И ВТ02.Вопрос = ВТ03_Отбор.Вопрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗвонкиСтат.Сигнатура КАК СигнатураЗвонка,
	|	Звонки.Дата КАК ДатаЗвонка,
	|	ВТ04.СигнатураЗадачи КАК СигнатураЗадачи
	|ПОМЕСТИТЬ ВТ05_Звонки_ИхМожетБытьНесколькоНаОднуЗадачу
	|ИЗ
	|	РегистрСведений.ЗвонкиСтатОбщая КАК ЗвонкиСтат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Звонки КАК Звонки
	|		ПО ЗвонкиСтат.Сигнатура = Звонки.Сигнатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ04_БезЗвонков КАК ВТ04
	|		ПО ЗвонкиСтат.Основание = ВТ04.ОписаниеЗадачиКакОснованиеЗвонка
	|			И (Звонки.Принят = ИСТИНА)
	|			И (НАЧАЛОПЕРИОДА(Звонки.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(Звонки.Дата, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ05.СигнатураЗадачи КАК СигнатураЗадачи,
	|	МАКСИМУМ(ВТ05.ДатаЗвонка) КАК ДатаЗвонка
	|ПОМЕСТИТЬ ВТ06_ДатыПозднейшегоЗвонкаПоЗадаче
	|ИЗ
	|	ВТ05_Звонки_ИхМожетБытьНесколькоНаОднуЗадачу КАК ВТ05
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ05.СигнатураЗадачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ05.СигнатураЗвонка КАК СигнатураЗвонка,
	|	ВТ05.ДатаЗвонка КАК ДатаЗвонка,
	|	ВТ05.СигнатураЗадачи КАК СигнатураЗадачи
	|ПОМЕСТИТЬ ВТ07_ОднаЗадачаПоследнийЗвонок
	|ИЗ
	|	ВТ05_Звонки_ИхМожетБытьНесколькоНаОднуЗадачу КАК ВТ05
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ06_ДатыПозднейшегоЗвонкаПоЗадаче КАК ВТ06
	|		ПО ВТ05.ДатаЗвонка = ВТ06.ДатаЗвонка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ04.Точка КАК Точка,
	|	ВТ04.Номер КАК Номер,
	|	ВТ04.Дата КАК Дата,
	|	ВТ04.Сотрудник КАК Сотрудник,
	|	ВТ04.Автомобиль КАК Автомобиль,
	|	ВТ04.Телефон КАК Телефон,
	|	ВТ04.КонтактноеЛицо КАК КонтактноеЛицо,
	|	ВТ04.СигнатураЗадачи КАК СигнатураЗадачи,
	|	ВТ04.Вопрос КАК Вопрос,
	|	ВТ04.Ответ КАК Ответ,
	|	ВТ04.ОтветКомментарий КАК ОтветКомментарий,
	|	ВТ07.СигнатураЗвонка КАК СигнатураЗвонка
	|ИЗ
	|	ВТ04_БезЗвонков КАК ВТ04
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ07_ОднаЗадачаПоследнийЗвонок КАК ВТ07
	|		ПО ВТ04.СигнатураЗадачи = ВТ07.СигнатураЗадачи";
	Запрос.УстановитьПараметр("ДатаНачалаПериодаЗакрытияЗадач",	ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаНачалаПериодаЗакрытияЗадач")).Значение); 
	Запрос.УстановитьПараметр("ДатаКонцаПериодаЗакрытияЗадач",	ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаКонцаПериодаЗакрытияЗадач")).Значение); 
	Запрос.УстановитьПараметр("Вопрос1", 						ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Вопрос1")).Значение);
	
	Результат = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	//Если РезультатЗапроса.Пустой() Тогда
	//	Возврат ТЗ;
	//КонецЕсли;
	
	ТЗ = Результат.Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура();
	ВнешниеНаборыДанных.Вставить("ТЗ", ТЗ);
	
	СКД = ПолучитьМакет("МакетНаТЗ");
	Настройки = СКД.НастройкиПоУмолчанию;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, Настройки, ДанныеРасшифровки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	

	
							
КонецПроцедуры                                

