&НаКлиенте
Процедура ПриОткрытии(Отказ)

// В списке "КонтактЦентры" могут присутствовать только элементы определенного типа
//-------------------------------------------------------------------------------------------------
	ЭтаФорма.КонтактЦентры.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.КонтактЦентры");

// Настроим параметры выборки для отчета
//-------------------------------------------------------------------------------------------------
	КонтактЦентрыПриИзменении(Неопределено);
	ПериодОтчетаПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ,СтандартнаяОбработка)
	ЭтаФорма.ВариантМодифицирован = Ложь;
КонецПроцедуры

//-------------------------------------------------------------------------------------------------

&НаКлиенте
Процедура ПериодОтчетаПриИзменении(Элемент)

// Если реквизиты начала и окончания периода заполнены, то перенесем их в компоновщик настроек
//-------------------------------------------------------------------------------------------------
	Доступно = ЗначениеЗаполнено(ЭтаФорма.ПериодОтчета.ДатаНачала);
	Доступно = Доступно И (ЭтаФорма.ПериодОтчета.ДатаОкончания >= ЭтаФорма.ПериодОтчета.ДатаНачала);

	Если (Доступно = Истина) Тогда
		Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачПериода",НачалоДня(ЭтаФорма.ПериодОтчета.ДатаНачала));
		Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КонПериода",НачалоДня(ЭтаФорма.ПериодОтчета.ДатаОкончания));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КонтактЦентрыПриИзменении(Элемент)

// Для начала удалим отбор по полю "КонтактЦентр" из компоновщика настроек
//-------------------------------------------------------------------------------------------------
	ИскомоеПоле = Новый ПолеКомпоновкиДанных("КонтактЦентр");

	Для Каждого ТекЭлемент Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если (ТекЭлемент.ЛевоеЗначение = ИскомоеПоле) Тогда
			Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;

// Если реквизит "КонтактЦентры" заполнен, то настроим фильтрацию по полю "КонтактЦентр"
//-------------------------------------------------------------------------------------------------
	Если (ЭтаФорма.КонтактЦентры.Количество() > 0) Тогда
		НовЭлемент = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		НовЭлемент.ПравоеЗначение = ЭтаФорма.КонтактЦентры;
		НовЭлемент.ЛевоеЗначение = ИскомоеПоле;
		НовЭлемент.Использование = Истина;
	КонецЕсли;
КонецПроцедуры
