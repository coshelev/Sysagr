
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТЗ = Сформировать();
	
	ВнешниеНаборыДанных = Новый Структура();
	ВнешниеНаборыДанных.Вставить("ТЗ", ТЗ);
	
	СКД = ПолучитьМакет("МакетНаТЗ");
	Настройки = СКД.НастройкиПоУмолчанию;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, Настройки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ПоказатьУровеньГруппировокСтрок(1);

 КонецПроцедуры
 
Процедура ЗаписатьИерархию() Экспорт
	

	Если ЭтотОбъект.Иерархия.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим, что один и тот же сайт не встречается более одного раза
	//------------------------------------------------------------------
	Запрос = Новый  Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЗ.Сайт КАК Сайт
	|ПОМЕСТИТЬ ВТ01
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ01.Сайт КАК Сайт,
	|	СУММА(1) КАК Поле1
	|ИЗ
	|	ВТ01 КАК ВТ01
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ01.Сайт
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1";
	Запрос.УстановитьПараметр("ТЗ", ЭтотОбъект.Иерархия);
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если Не РезультатЗапроса.Пустой() Тогда
		Сообщить("Один и тот же сайт повторяется в разных группах. Иерархия не сохранена.");
		Возврат
	КонецЕсли;
	
	
	Зап = Новый ЗаписьXML();
	Зап.УстановитьСтроку();
	Зап.ЗаписатьОбъявлениеXML();
	Зап.ЗаписатьНачалоЭлемента("root"); 	// <root>

	Для Каждого СтрТЧ Из ЭтотОбъект.Иерархия Цикл
		Зап.ЗаписатьНачалоЭлемента("row");  	//<row>

			Зап.ЗаписатьНачалоЭлемента("group");	//<group>
			Зап.ЗаписатьТекст(СтрТЧ.Группа);
			Зап.ЗаписатьКонецЭлемента(); 			//</group>

			Зап.ЗаписатьНачалоЭлемента("site");		//<site>
			Зап.ЗаписатьТекст(СтрТЧ.Сайт);
			Зап.ЗаписатьКонецЭлемента(); 			//</site>

	    Зап.ЗаписатьКонецЭлемента(); 			//</row>

	КонецЦикла;
	
	Зап.ЗаписатьКонецЭлемента(); 			 //	</root>

	Стр=Зап.Закрыть();
	
	Если ЭтотОбъект.ИерархияСтрокой = Стр Тогда
		Возврат
	КонецЕсли;
	
	ТекДата = ТекущаяДата();
	НЗ = РегистрыСведений.УчетнаяПолитика.СоздатьНаборЗаписей();
	НЗ.Отбор.Период.Установить(ТекДата);
	НЗ.Отбор.Параметр.Установить("Отчет_ПрогнозЗадачПоИнтернетЗаявкам_Иерархия");
	НЗ.Прочитать();
	
	Новая = НЗ.Добавить();
	Новая.Период 			= ТекДата;
	Новая.Параметр 			= "Отчет_ПрогнозЗадачПоИнтернетЗаявкам_Иерархия";
	Новая.ТипЗначения 			= "Строка";
	Новая.Значение 			= Стр;
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		НЗ.Записать();
	Исключение
		Данные = "Стр = "+Стр;
		Комментарий = ОписаниеОшибки();
		ОбслуживаниеСервер.ЗарегистрироватьСобытие("Записать параметр учетной политики", УровеньЖурналаРегистрации.Ошибка, Данные, Комментарий,,,,ИмяКомпьютера());
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция СформироватьБазовуюТаблицуЗадачиИнтернетЗаявки (ДатаНачала, ДатаОкончания)    
	
	ЭтотОбъект.ЗадачиИнтернетЗаявки.Очистить();
	ЗначениеПоУмолчанию = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Заявки.Сигнатура КАК Сигнатура,
	|	Заявки.АбонентВнешний КАК Телефон,
	|	НАЧАЛОПЕРИОДА(Заявки.Дата, МЕСЯЦ) КАК Дата,
	|	Задачи.Инициатор КАК Инициатор,
	|	ЕСТЬNULL(Свойства.Значение.Наименование, &ПустаяСтрока) КАК ИсточникЗаявки,
	|	1 КАК Количество
	|ИЗ
	|	РегистрСведений.ЗадачиИнтернетЗаявки КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИнтернетЗаявки КАК Заявки
	|		ПО Задачи.Сигнатура = Заявки.Сигнатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнтернетЗаявкиСвойства КАК Свойства
	|		ПО (Заявки.Сигнатура = Свойства.Сигнатура)
	|			И (Свойства.Свойство.Наименование = &ИмяСвойства)
	|ГДЕ
	|	Задачи.ДатаПостановки МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)";
	
	Запрос.УстановитьПараметр("ДатаНачала", 	ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ДатаОкончания);
	Запрос.УстановитьПараметр("ИмяСвойства",	"referrer_source");
	Запрос.УстановитьПараметр("ПустаяСтрока",	"<источник заявки не определен>");
		
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Новая = ЭтотОбъект.ЗадачиИнтернетЗаявки.Добавить();
		Новая.Сигнатура 		= Выборка.Сигнатура;
		Новая.Телефон			= Выборка.Телефон;
		Новая.Дата				= Выборка.Дата;
		Новая.Инициатор			= Выборка.Инициатор;
		Новая.ИсточникЗаявки	= Выборка.ИсточникЗаявки
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

Функция СформироватьБазовуюТаблицуЦелевыеЗвонкиНаОснованииИнтернетЗаявок(ДатаНачала, ДатаОкончания)
	
	ЭтотОбъект.ЦелевыеЗвонки.Очистить();
	ЭтотОбъект.ЦелевыеЗвокиНаОснованииИнтернетЗаявок.Очистить();
		
	ЗначениеПоУмолчанию = Ложь;
		
	//Прочитай данные о сделках из ОУ
	//-------------------------------
	ИсточникДанных = Справочники.ИБ_ИсточникиДанных.НайтиПоКоду("000000002");
	Если ИсточникДанных = Неопределено Тогда
		Сообщить("Нет подключения к базе оперативного учета");
		Возврат ЗначениеПоУмолчанию
	Иначе
		ПараметрыСоединения = ИсточникДанных.ПараметрыСоединения;
		БИ = ОбщегоНазначения.ПолучитьПодключениеБД(ПараметрыСоединения);	
	КонецЕсли;

	БИ_Запрос = БИ.NewObject("Запрос");
	БИ_Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	ЗвонкиЦелевые.Код КАК Сигнатура,
	|	ПРЕДСТАВЛЕНИЕ(ЗвонкиЦелевые.Состояние) КАК Статус
	|ИЗ
	|	Справочник.Звонки КАК ЗвонкиЦелевые
	|ГДЕ
	|	ЗвонкиЦелевые.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаОкончания, ДЕНЬ, 10), ДЕНЬ)
	|	И (ЗвонкиЦелевые.Состояние = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ОбщийНовый)
	|			ИЛИ ЗвонкиЦелевые.Состояние = ЗНАЧЕНИЕ(Справочник.СтатусыОбъектов.ОбщийИсполнен)
	|				И ЗвонкиЦелевые.Основание ССЫЛКА Документ.СделкаРеализации
	|				И ЗвонкиЦелевые.Дата <= ЗвонкиЦелевые.Основание.Дата)";	
	БИ_Запрос.УстановитьПараметр("ДатаНачала",				ДатаНачала);
	БИ_Запрос.УстановитьПараметр("ДатаОкончания", 			ДатаОкончания);	
	
	БИ_РезультатЗапроса = БИ_Запрос.Выполнить();	
	БИ_Выборка = БИ_Запрос.Выполнить().Выбрать();
	
	Пока БИ_Выборка.Следующий() Цикл
		Новая = ЭтотОбъект["ЦелевыеЗвонки"].Добавить();	
		Новая.Сигнатура = БИ_Выборка.Сигнатура;
		Новая.Статус	= БИ_Выборка.Статус;
	КонецЦикла;

	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.Сигнатура КАК Сигнатура
	|ПОМЕСТИТЬ ВТ00_ЦелевыеЗвонки
	|ИЗ
	|	&РеестрЦелевых КАК ТЧ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ00.Сигнатура КАК Сигнатура,
	|	ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 4, 36) КАК Основание
	|ПОМЕСТИТЬ ВТ01_ЦелевыеНаОснованииИнтернетЗаявок
	|ИЗ
	|	РегистрСведений.ЗвонкиСтатОбщая КАК ЗвонкиСтатОбщая
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ00_ЦелевыеЗвонки КАК ВТ00
	|		ПО ЗвонкиСтатОбщая.Сигнатура = ВТ00.Сигнатура
	|ГДЕ
	|	ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 1, 2) = &ПрефиксСигнатурыИнтернетЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиИнтернетЗаявки.Сигнатура КАК СигнатураЗаявки
	|ПОМЕСТИТЬ ВТ02_ЗадачиИнтернетЗаявки
	|ИЗ
	|	&ЗадачиИнтернетЗаявки КАК ЗадачиИнтернетЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ01.Сигнатура КАК СигнатураЗвонка,
	|	ВТ02.СигнатураЗаявки КАК СигнатураЗаявки
	|ИЗ
	|	ВТ01_ЦелевыеНаОснованииИнтернетЗаявок КАК ВТ01
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ02_ЗадачиИнтернетЗаявки КАК ВТ02
	|		ПО ВТ01.Основание = ВТ02.СигнатураЗаявки";
	
	Запрос.УстановитьПараметр("РеестрЦелевых", ЭтотОбъект.ЦелевыеЗвонки);
	Запрос.УстановитьПараметр("ЗадачиИнтернетЗаявки", ЭтотОбъект.ЗадачиИнтернетЗаявки);
	Запрос.УстановитьПараметр("ПрефиксСигнатурыИнтернетЗаявки", "iq");
	
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ЗначениеПоУмолчанию
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Новая = ЭтотОбъект.ЦелевыеЗвокиНаОснованииИнтернетЗаявок.Добавить();
		Новая.СигнатураЗвонка 	= Выборка.СигнатураЗвонка;
		Новая.СигнатураЗаявки	= Выборка.СигнатураЗаявки;
	КонецЦикла;
	
	Возврат Истина
	
КонецФункции

Функция ВходящиеЗвонкиНаКорпоративныйОтдел (ДатаНачала, ДатаОкончания, СписокТелефоновКорпоративногоОтдела)    

	
	ЭтотОбъект.ВходящиеЗвонкиКорпоративногоОтдела.Очистить();
	ЗначениеПоУмолчанию = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗвонкиСтатОбщая.Сигнатура КАК Сигнатура,
	|	ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 4, 36) КАК СигнатураЗаявки
	|ИЗ
	|	РегистрСведений.Звонки КАК Звонки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗвонкиСтатОбщая КАК ЗвонкиСтатОбщая
	|		ПО Звонки.Сигнатура = ЗвонкиСтатОбщая.Сигнатура
	|ГДЕ
	|	Звонки.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 1, 2) = &ПрефиксСигнатурыИнтернетЗаявки
	|	И Звонки.АбонентВнутренний.Код В(&СписокТелефоновКорпоративногоОтдела)";
	
	Запрос.УстановитьПараметр("ДатаНачала", 						 ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 						 ДатаОкончания);
	Запрос.УстановитьПараметр("ПрефиксСигнатурыИнтернетЗаявки", 	 "iq");
	Запрос.УстановитьПараметр("СписокТелефоновКорпоративногоОтдела", СписокТелефоновКорпоративногоОтдела);	
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Новая = ЭтотОбъект.ВходящиеЗвонкиКорпоративногоОтдела.Добавить();
		Новая.СигнатураЗвонка = Выборка.Сигнатура;
		Новая.СигнатураЗаявки = Выборка.СигнатураЗаявки;
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

Функция ЦелевыеЗвокиПредположительноНаОснованииИнтернетЗаявок (ДатаНачала, ДатаОкончания)    

	
	ЭтотОбъект. ПредполагаемыеЗаявкиЦелевыхБезЗаявок.Очистить();
	ЗначениеПоУмолчанию = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т01.Сигнатура КАК Сигнатура
	|ПОМЕСТИТЬ ВТ01_ВсеЦелевые
	|ИЗ
	|	&Т01 КАК Т01
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т02.СигнатураЗвонка КАК Сигнатура
	|ПОМЕСТИТЬ ВТ02_ЦелевыеНаОснованииЗаявок
	|ИЗ
	|	&Т02 КАК Т02
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т03.СигнатураЗвонка КАК Сигнатура
	|ПОМЕСТИТЬ ВТ03_ВходящиеКорпоративногоОтдела
	|ИЗ
	|	&Т03 КАК Т03
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ01_ВсеЦелевые.Сигнатура КАК Сигнатура
	|ПОМЕСТИТЬ ВТ04_ЦелевыеНеПоЗаявкам
	|ИЗ
	|	ВТ01_ВсеЦелевые КАК ВТ01_ВсеЦелевые
	|ГДЕ
	|	НЕ(ВТ01_ВсеЦелевые.Сигнатура В
	|					(ВЫБРАТЬ
	|						ВТ02.Сигнатура
	|					ИЗ
	|						ВТ02_ЦелевыеНаОснованииЗаявок КАК ВТ02)
	|				ИЛИ ВТ01_ВсеЦелевые.Сигнатура В
	|					(ВЫБРАТЬ
	|						ВТ03.Сигнатура
	|					ИЗ
	|						ВТ03_ВходящиеКорпоративногоОтдела КАК ВТ03))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ04_ЦелевыеНеПоЗаявкам.Сигнатура КАК Сигнатура,
	|	Звонки.АбонентВнешний КАК АбонентВнешний,
	|	Звонки.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ05_ЦелевыеНеПоЗаявкам
	|ИЗ
	|	ВТ04_ЦелевыеНеПоЗаявкам КАК ВТ04_ЦелевыеНеПоЗаявкам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Звонки КАК Звонки
	|		ПО ВТ04_ЦелевыеНеПоЗаявкам.Сигнатура = Звонки.Сигнатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Звонки.Сигнатура КАК Сигнатура,
	|	Звонки.АбонентВнешний КАК АбонентВнешний,
	|	Звонки.Дата КАК Дата,
	|	ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 4, 36) КАК СигнатураЗаявки
	|ПОМЕСТИТЬ ВТ06_ИсходящиеПоЗаявкамНеСтавшиеЦелевыми
	|ИЗ
	|	РегистрСведений.Звонки КАК Звонки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗвонкиСтатОбщая КАК ЗвонкиСтатОбщая
	|		ПО Звонки.Сигнатура = ЗвонкиСтатОбщая.Сигнатура
	|ГДЕ
	|	Звонки.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ПОДСТРОКА(ЗвонкиСтатОбщая.Основание, 1, 2) = &ПрефиксСигнатурыИнтернетЗаявки
	|	И НЕ Звонки.Сигнатура В
	|				(ВЫБРАТЬ
	|					ВТ05.Сигнатура
	|				ИЗ
	|					ВТ05_ЦелевыеНеПоЗаявкам КАК ВТ05)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ05.Сигнатура КАК СигнатураЗвонкаЦелевогоНеПоЗаявке,
	|	ВТ05.АбонентВнешний КАК АбонентВнешний,
	|	ВТ05.Дата КАК ДатаЗвонкаЦелевогоНеПоЗаявке,
	|	ВТ06.СигнатураЗаявки КАК СигнатураЗаявки,
	|	ВТ06.Сигнатура КАК СигнатураЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым,
	|	ВТ06.Дата КАК ДатаЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым
	|ПОМЕСТИТЬ ВТ07_ВсеСочетанияЦелевойНеПоЗаявке_2_НецелевойИсходящийПоЗаявке
	|ИЗ
	|	ВТ05_ЦелевыеНеПоЗаявкам КАК ВТ05
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ06_ИсходящиеПоЗаявкамНеСтавшиеЦелевыми КАК ВТ06
	|		ПО ВТ05.АбонентВнешний = ВТ06.АбонентВнешний
	|			И ВТ05.Дата > ВТ06.Дата
	|			И (РАЗНОСТЬДАТ(ВТ06.Дата, ВТ05.Дата, ЧАС) <= &НормаРазностиДат)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ07.СигнатураЗвонкаЦелевогоНеПоЗаявке КАК СигнатураЗвонкаЦелевогоНеПоЗаявке,
	|	ВТ07.ДатаЗвонкаЦелевогоНеПоЗаявке КАК ДатаЗвонкаЦелевогоНеПоЗаявке,
	|	ВТ07.АбонентВнешний КАК АбонентВнешнийСправочно,
	|	ВТ07.СигнатураЗаявки КАК СигнатураЗаявки,
	|	ВТ07.СигнатураЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым КАК СигнатураЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым,
	|	ВТ07.ДатаЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым КАК ДатаЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым
	|ИЗ
	|	ВТ07_ВсеСочетанияЦелевойНеПоЗаявке_2_НецелевойИсходящийПоЗаявке КАК ВТ07
	|ИТОГИ ПО
	|	СигнатураЗаявки";
	
	Запрос.УстановитьПараметр("Т01", 								ЦелевыеЗвонки);
	Запрос.УстановитьПараметр("Т02", 								ЦелевыеЗвокиНаОснованииИнтернетЗаявок);
	Запрос.УстановитьПараметр("Т03", 								ВходящиеЗвонкиКорпоративногоОтдела);
	Запрос.УстановитьПараметр("ДатаНачала", 						ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 						ДатаОкончания);
	Запрос.УстановитьПараметр("ПрефиксСигнатурыИнтернетЗаявки", 	"iq");
	Запрос.УстановитьПараметр("НормаРазностиДат",					24);	
	
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		Новая = ПредполагаемыеЗаявкиЦелевыхБезЗаявок.Добавить();
		Новая.СигнатураЗаявки = Выборка.СигнатураЗаявки;
		
		//Выбери одну подчиненную запись
		//------------------------------
		ПодВыборка= Выборка.Выбрать();
		Пока ПодВыборка.Следующий() Цикл
			Новая.СигнатураЗвонкаЦелевогоНеПоЗаявке 					= ПодВыборка.СигнатураЗвонкаЦелевогоНеПоЗаявке;
			Новая.ДатаЗвонкаЦелевогоНеПоЗаявке							= ПодВыборка.ДатаЗвонкаЦелевогоНеПоЗаявке;
			Новая.СигнатураЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым 	= ПодВыборка.СигнатураЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым;
			Новая.ДатаЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым			= ПодВыборка.ДатаЗвонкаИсходящегоПоЗаявкеНеСтавшегоЦелевым;
			Прервать;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

Функция Сформировать() Экспорт
	
	ТЗ = Новый ТаблицаЗначений();
	
	Период 						=	ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")).Значение;

	ДатаНачала = Период.ДатаНачала;
	ДатаКонца  = Период.ДатаОкончания;
	
	//Заполни ТЧ ЗадачиИнтенетЗаявки
	//---------------------------------
	Рез = СформироватьБазовуюТаблицуЗадачиИнтернетЗаявки(ДатаНачала, ДатаКонца);
	Если Рез = Ложь Тогда
		Сообщить("Ошибка заполнения таблицы Задачи по интернет-заявкам");
		Возврат ТЗ;
	КонецЕсли;
	
	//Заполни ТЧ ЦелевыеЗвонкиОУ и ТЧ елевыеЗвонкиНаОснованииИнтернетЗаявок
	//---------------------------------------------------------------------
	Рез = СформироватьБазовуюТаблицуЦелевыеЗвонкиНаОснованииИнтернетЗаявок(ДатаНачала, ДатаКонца);
	Если Рез = Ложь Тогда
		Сообщить("Ошибка заполнения таблицы ""Целевые звонки ОУ"" или ""Целевые звонки на основании интернет - заявок"" ");
		Возврат ТЗ;
	КонецЕсли;

	//Заполни ТЧ ВходящиеЗвонкиНаКорпоративныйОтдел
	//----------------------------------------------
	ТелефоныКорпоративногоОтдела =	ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТелефоныКорпоративногоОтдела")).Значение;
	Доступно = ТипЗнч(ТелефоныКорпоративногоОтдела) = Тип("СписокЗначений");
	Доступно = Доступно И ТелефоныКорпоративногоОтдела.Количество()>0;
	Если Доступно Тогда
		Рез = ВходящиеЗвонкиНаКорпоративныйОтдел(ДатаНачала, ДатаКонца, ТелефоныКорпоративногоОтдела);
	КонецЕсли;
	
	// Заполни ТЧ ЦелевыеЗвокиПредположительноНаОснованииИнтернетЗаявок
	//-----------------------------------------------------------------
	ЦелевыеЗвокиПредположительноНаОснованииИнтернетЗаявок(ДатаНачала, ДатаКонца);

	// В запросе к каждой группировке цепляем все возможные значения свойства referrer_source (источник заявки), 
	//и к одному из этих значений прикрепим стоимость компаний сайта
	//----------------------------------------------------------------------------------------------------------
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Свойства.Значение.Наименование КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ003_ВсеСвойства
	|ИЗ
	|	РегистрСведений.ИнтернетЗаявкиСвойства КАК Свойства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиИнтернетЗаявки КАК Задачи
	|		ПО Свойства.Сигнатура = Задачи.Сигнатура
	|ГДЕ
	|	Свойства.Свойство.Наименование = &ИмяСвойства
	|	И Задачи.ДатаПостановки МЕЖДУ &ПредДатаНачала И &ДатаКонца
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	""<источник заявки не определен>""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ003.ИсточникЗаявки КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ004_ОдноЛюбоеСвойствоДляПрикрепленияСтоимости
	|ИЗ
	|	ВТ003_ВсеСвойства КАК ВТ003
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Заявки.Дата, МЕСЯЦ) КАК Дата,
	|	Задачи.Инициатор КАК Инициатор,
	|	ЕСТЬNULL(Свойства.Значение.Наименование, ""<источник заявки не определен>"") КАК ИсточникЗаявки,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ВТ01_ПредПериод
	|ИЗ
	|	РегистрСведений.ЗадачиИнтернетЗаявки КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИнтернетЗаявки КАК Заявки
	|		ПО Задачи.Сигнатура = Заявки.Сигнатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнтернетЗаявкиСвойства КАК Свойства
	|		ПО (Заявки.Сигнатура = Свойства.Сигнатура)
	|			И (Свойства.Свойство.Наименование = &ИмяСвойства)
	|ГДЕ
	|	Задачи.ДатаПостановки МЕЖДУ &ПредДатаНачала И &ПредДатаКонца
	|
	|СГРУППИРОВАТЬ ПО
	|	Задачи.Инициатор,
	|	НАЧАЛОПЕРИОДА(Заявки.Дата, МЕСЯЦ),
	|	ЕСТЬNULL(Свойства.Значение.Наименование, ""<источник заявки не определен>"")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ01.Дата КАК Дата,
	|	ВТ01.Инициатор КАК Инициатор,
	|	ВТ003.ИсточникЗаявки КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ011_ВсеСочетанияСайтИсточникПред
	|ИЗ
	|	ВТ01_ПредПериод КАК ВТ01
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ003_ВсеСвойства КАК ВТ003
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ011.Дата КАК Дата,
	|	ВТ011.Инициатор КАК Инициатор,
	|	ВТ011.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ЕСТЬNULL(ВТ01.Количество, 0) КАК Количество
	|ПОМЕСТИТЬ ВТ004_ПредПериод
	|ИЗ
	|	ВТ011_ВсеСочетанияСайтИсточникПред КАК ВТ011
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ01_ПредПериод КАК ВТ01
	|		ПО ВТ011.Дата = ВТ01.Дата
	|			И ВТ011.Инициатор = ВТ01.Инициатор
	|			И ВТ011.ИсточникЗаявки = ВТ01.ИсточникЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиИнтернетЗаявки.Сигнатура КАК Сигнатура,
	|	ЗадачиИнтернетЗаявки.Телефон КАК Телефон,
	|	ЗадачиИнтернетЗаявки.Дата КАК Дата,
	|	ЗадачиИнтернетЗаявки.Инициатор КАК Инициатор,
	|	ЗадачиИнтернетЗаявки.ИсточникЗаявки КАК ИсточникЗаявки,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ВТ0041_ЗадачиИнтернетЗаявки
	|ИЗ
	|	&ЗадачиИнтернетЗаявки КАК ЗадачиИнтернетЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦелевыеНаОсновании.СигнатураЗаявки КАК СигнатураЗаявки,
	|	1 КАК КоличествоЦелевыхЗаявок
	|ПОМЕСТИТЬ ВТ0042_ЦелевыеЗаявки
	|ИЗ
	|	&ЦелевыеЗвокиНаОснованииИнтернетЗаявок КАК ЦелевыеНаОсновании
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ0041.Сигнатура КАК Сигнатура,
	|	ВТ0041.Дата КАК Дата,
	|	ВТ0041.Инициатор КАК Инициатор,
	|	ВТ0041.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ВТ0041.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ВТ0041.Телефон ПОДОБНО &Тел0000000000
	|			ТОГДА 1
	|		КОГДА ВТ0041.Сигнатура В (&ЗаявкиОснованияВходящихЗвонковКорпоративногоОтдела)
	|			ТОГДА 1
	|		КОГДА ВТ0041.Сигнатура В (&ПредполагаемыеЗаявкиЦелевыхБезЗаявок)
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ВТ0042.КоличествоЦелевыхЗаявок, 0)
	|	КОНЕЦ КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ0043_ЗадачиИнтернетЗаявки
	|ИЗ
	|	ВТ0041_ЗадачиИнтернетЗаявки КАК ВТ0041
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ0042_ЦелевыеЗаявки КАК ВТ0042
	|		ПО ВТ0041.Сигнатура = ВТ0042.СигнатураЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Задачи.Дата, МЕСЯЦ) КАК Дата,
	|	Задачи.Инициатор КАК Инициатор,
	|	Задачи.ИсточникЗаявки КАК ИсточникЗаявки,
	|	СУММА(Задачи.Количество) КАК Количество,
	|	СУММА(Задачи.КоличествоЦелевых) КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ02_ТекПериод
	|ИЗ
	|	ВТ0043_ЗадачиИнтернетЗаявки КАК Задачи
	|
	|СГРУППИРОВАТЬ ПО
	|	Задачи.Инициатор,
	|	НАЧАЛОПЕРИОДА(Задачи.Дата, МЕСЯЦ),
	|	Задачи.ИсточникЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ02.Дата КАК Дата,
	|	ВТ02.Инициатор КАК Инициатор,
	|	ВТ003.ИсточникЗаявки КАК ИсточникЗаявки
	|ПОМЕСТИТЬ ВТ021_ВсеСочетанияСайтИсточникТек
	|ИЗ
	|	ВТ02_ТекПериод КАК ВТ02
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ003_ВсеСвойства КАК ВТ003
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ21.Дата КАК Дата,
	|	ВТ21.Инициатор КАК Инициатор,
	|	ВТ21.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ЕСТЬNULL(ВТ02.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТ02.КоличествоЦелевых, 0) КАК КоличествоЦелевых
	|ПОМЕСТИТЬ ВТ022_ТекПериод
	|ИЗ
	|	ВТ021_ВсеСочетанияСайтИсточникТек КАК ВТ21
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ02_ТекПериод КАК ВТ02
	|		ПО ВТ21.Дата = ВТ02.Дата
	|			И ВТ21.Инициатор = ВТ02.Инициатор
	|			И ВТ21.ИсточникЗаявки = ВТ02.ИсточникЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ02.Дата, ВТ01.Дата) КАК ДатаТек,
	|	ЕСТЬNULL(ВТ02.Инициатор, ВТ01.Инициатор) КАК Инициатор,
	|	ЕСТЬNULL(ВТ02.ИсточникЗаявки, ВТ01.ИсточникЗаявки) КАК ИсточникЗаявки,
	|	ЕСТЬNULL(ВТ02.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТ02.КоличествоЦелевых, 0) КАК КоличествоЦелевых,
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, ДЕНЬ) + 1 > 0
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВТ02.Количество, 0) / (РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, ДЕНЬ) + 1) * (РАЗНОСТЬДАТ(&ДатаНачала, КОНЕЦПЕРИОДА(&ДатаНачала, МЕСЯЦ), ДЕНЬ) + 1) КАК ЧИСЛО(10, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПрогноз,
	|	ЕСТЬNULL(ВТ01.Количество, 0) КАК КоличествоПред,
	|	ЕСТЬNULL(ВТ01.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПред
	|ПОМЕСТИТЬ ВТ04
	|ИЗ
	|	ВТ022_ТекПериод КАК ВТ02
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ004_ПредПериод КАК ВТ01
	|		ПО ВТ02.Инициатор = ВТ01.Инициатор
	|			И (ВТ01.ИсточникЗаявки = ВТ02.ИсточникЗаявки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СайтыКомпании.Ссылка.Наименование, ""<не найден сайт для компании>"") КАК Сайт,
	|	ВЫБОР
	|		КОГДА Рег.Сервис.Наименование = ""Google AdWords""
	|			ТОГДА ""Контекстная реклама Google""
	|		КОГДА Рег.Сервис.Наименование = ""direct.yandex.ru""
	|			ТОГДА ""Контекстная реклама Яндекс""
	|		ИНАЧЕ ""<источник заявки не определен>""
	|	КОНЕЦ КАК ИсточникЗаявки,
	|	СУММА(ВЫБОР
	|			КОГДА Рег.Сервис.Наименование = ""Google AdWords""
	|				ТОГДА Рег.Стоимость * &Курс * 1.2
	|			ИНАЧЕ Рег.Стоимость
	|		КОНЕЦ) КАК Стоимость
	|ПОМЕСТИТЬ ВТ0011_СтоимостиЗаявокПоСайтамПоПериодам
	|ИЗ
	|	РегистрСведений.ИнтернетЗаявкиСтоимость КАК Рег
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сайты.Компании КАК СайтыКомпании
	|		ПО Рег.ИдКомпании = СайтыКомпании.Ид
	|ГДЕ
	|	Рег.ПериодЗаявки МЕЖДУ &ДатаНачала И &ДатаКонца
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(СайтыКомпании.Ссылка.Наименование, ""<не найден сайт для компании>""),
	|	ВЫБОР
	|		КОГДА Рег.Сервис.Наименование = ""Google AdWords""
	|			ТОГДА ""Контекстная реклама Google""
	|		КОГДА Рег.Сервис.Наименование = ""direct.yandex.ru""
	|			ТОГДА ""Контекстная реклама Яндекс""
	|		ИНАЧЕ ""<источник заявки не определен>""
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ04.ДатаТек КАК ДатаТек,
	|	ВТ04.Инициатор КАК Инициатор,
	|	ВТ04.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ВТ04.Количество КАК Количество,
	|	ВТ04.КоличествоЦелевых КАК КоличествоЦелевых,
	|	ВТ04.КоличествоПрогноз КАК КоличествоПрогноз,
	|	ВТ04.КоличествоПред КАК КоличествоПред,
	|	ВТ04.ДатаПред КАК ДатаПред,
	|	ЕСТЬNULL(ВТ002.Стоимость, 0) КАК СтоимостьКомпанийСайта,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ002.Стоимость, 0) > 0
	|			ТОГДА ВТ04.Количество / ЕСТЬNULL(ВТ002.Стоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьЗаявки
	|ПОМЕСТИТЬ ВТ05
	|ИЗ
	|	ВТ04 КАК ВТ04
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ0011_СтоимостиЗаявокПоСайтамПоПериодам КАК ВТ002
	|		ПО ВТ04.Инициатор = ВТ002.Сайт
	|			И ВТ04.ИсточникЗаявки = ВТ002.ИсточникЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЗ.Группа КАК Группа,
	|	ТЗ.Сайт КАК Сайт
	|ПОМЕСТИТЬ ВТ00_Иерархия
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ05.ДатаТек КАК ДатаТек,
	|	ВТ05.ДатаПред КАК ДатаПред,
	|	ВТ05.Инициатор КАК Инициатор,
	|	ВТ05.ИсточникЗаявки КАК ИсточникЗаявки,
	|	ВТ05.Количество КАК Количество,
	|	ВТ05.КоличествоЦелевых КАК КоличествоЦелевых,
	|	ВТ05.КоличествоПрогноз КАК КоличествоПрогноз,
	|	ВТ05.КоличествоПред КАК КоличествоПред,
	|	ЕСТЬNULL(ВТ00.Группа, ""Прочие"") КАК Группа,
	|	ВТ05.СтоимостьКомпанийСайта КАК СтоимостьКомпанийСайта,
	|	ВТ05.СтоимостьЗаявки КАК СтоимостьЗаявки
	|ИЗ
	|	ВТ05 КАК ВТ05
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ00_Иерархия КАК ВТ00
	|		ПО ВТ05.Инициатор = ВТ00.Сайт" ;
	
	Период 		=  ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")).Значение;
	ПредПериод 	=  ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПредПериод")).Значение;
	
	Запрос.УстановитьПараметр("ПредДатаНачала", ПредПериод.ДатаНачала);
	Запрос.УстановитьПараметр("ПредДатаКонца",  ПредПериод.ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаНачала", 	Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца", 		Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ИмяСвойства",	"referrer_source");	
	Запрос.УстановитьПараметр("Курс",   ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Курс")).Значение);
	Запрос.УстановитьПараметр("ТЗ", ЭтотОбъект.Иерархия);
	Запрос.УстановитьПараметр("ЗадачиИнтернетЗаявки", ЭтотОбъект.ЗадачиИнтернетЗаявки);
	Запрос.УстановитьПараметр("ЦелевыеЗвокиНаОснованииИнтернетЗаявок", ЭтотОбъект.ЦелевыеЗвокиНаОснованииИнтернетЗаявок);
	Запрос.УстановитьПараметр("Тел0000000000", "0000000000%");
	Запрос.УстановитьПараметр("ЗаявкиОснованияВходящихЗвонковКорпоративногоОтдела", ЭтотОбъект.ВходящиеЗвонкиКорпоративногоОтдела.ВыгрузитьКолонку("СигнатураЗаявки"));
	Запрос.УстановитьПараметр("ПредполагаемыеЗаявкиЦелевыхБезЗаявок", ЭтотОбъект.ПредполагаемыеЗаявкиЦелевыхБезЗаявок.ВыгрузитьКолонку("СигнатураЗаявки"));
	
	РезультатЗапроса = ОбщегоНазначения.ВыполнитьЗапрос(Запрос);
	ТЗ = РезультатЗапроса.Выгрузить();
	Возврат ТЗ;
КонецФункции