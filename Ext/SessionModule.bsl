Процедура УстановкаПараметровСеанса(ТребуемыеПараметры)

	// Предварительная установка параметров сеанса по-умолчанию
	//-------------------------------------------------------------------------------------------------
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ИнтерактивныйРежим = Ложь;
	ПараметрыСеанса.ККЦОчередь 					= Справочники.ТелОчереди.ПустаяСсылка();
	ПараметрыСеанса.ККЦТелефон 					= Справочники.ТелВнутренние.ПустаяСсылка();
	ПараметрыСеанса.ККЦКонтактЦентр 			= Справочники.КонтактЦентры.ПустаяСсылка();
	ПараметрыСеанса.ТекущийПользователь 		= Справочники.Пользователи.ЛокальнаяСистема;
	ПараметрыСеанса.РольДоступнаАдминистратор	= РольДоступна("Администратор") ИЛИ РольДоступна("ТелефонияРегионы_Администратор");

	Оболочка = ПолучитьОбщийМакет("ЖурналФоновыхЗаданийПользователя");
	ПараметрыСеанса.ФонЗадания = Оболочка.ПолучитьТекст();

	// Сопоставим имя пользователя под которым выполнен вход с элементом справочника "Пользователи"
	//-------------------------------------------------------------------------------------------------
	Если (ЗначениеЗаполнено(ИмяПользователя())) Тогда
		СпрСсылка = Справочники.Пользователи.НайтиПоРеквизиту("УчетнаяЗаписьИБ",СокрЛП(ИмяПользователя()));
		ПараметрыСеанса.ТекущийПользователь = ?(ЗначениеЗаполнено(СпрСсылка),СпрСсылка,ПараметрыСеанса.ТекущийПользователь);
	КонецЕсли;
	
	Парам = Новый Структура("MySQL_Asterisk_ExternalDataSourceConnectionParameters");
	Если РегистрыСведений.УчетнаяПолитика.Получи(Парам) Тогда    
		Чт = Новый ЧтениеXML();
		Чт.УстановитьСтроку(Парам.MySQL_Asterisk_ExternalDataSourceConnectionParameters);
		Фабрика = ФабрикаXDTO.ПрочитатьXML(Чт);
		
		//Пароль немного зашифрован в хранилище, заменим левые символы "_AND_" на правильный "&"
		//--------------------------------------------------------------------------------------
		Фабрика.Password = СтрЗаменить(Фабрика.Password, "_AND_", "&"); 
		
		СтрПараметры = Новый ПараметрыСоединенияВнешнегоИсточникаДанных; 
		ЗаполнитьЗначенияСвойств(СтрПараметры, Фабрика );
	    ВнешниеИсточникиДанных.AsteriskNnov.УстановитьОбщиеПараметрыСоединения(СтрПараметры);
	КонецЕсли;
	
КонецПроцедуры                       
