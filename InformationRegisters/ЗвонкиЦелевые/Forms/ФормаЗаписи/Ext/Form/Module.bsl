&НаСервере
Процедура ПриСозданииНаСервере(Отказ,СтандартнаяОбработка)

// Получим сведения о звонке, на который ссылается сигнатура текущей записи
//-------------------------------------------------------------------------------------------------
	Звонок = РегистрыСведений.Звонки.СоздатьМенеджерЗаписи();
	Звонок.Сигнатура = СокрЛП(Запись.Сигнатура);
	Звонок.Прочитать();

	Если (Звонок.Выбран() = Ложь) Тогда
		Сообщить("В регистре ""Звонки"" не найдена запись соответствующая сигнатуре выбранного звонка");
		Отказ = Истина;
		Возврат;
	КонецЕсли;

// Сформируем заголовок и доступность формы
//-------------------------------------------------------------------------------------------------
	ЭтаФорма.Заголовок = "Целевой " + ?(Звонок.ЭтоВходящий,"входящий","исходящий");
	ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + " от " + СокрЛП(Формат(Звонок.Дата,"ДЛФ=DDT"));
	ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + ", " + Конвертация.ДеньНеделиПрописью(Звонок.Дата);
	ЭтаФорма.ТолькоПросмотр = Истина;

// Установим видимость элементов формы в зависимости от значения реквизита "Собственный"
//-------------------------------------------------------------------------------------------------
	Элементы.НадписьСобственный.Видимость = (Запись.Собственный = Истина);

// Получим маршрут звонка
//-------------------------------------------------------------------------------------------------
	ТекстЗапроса = "
	|SELECT	Рег.Дата,Рег.ИмяСобытия,Рег.Оператор,Прив.Владелец.ТочкаРазмещения AS ТочкаРазмещения,
	|		CASE WHEN (Рег.Оператор ССЫЛКА Справочник.ТелОчереди) THEN Рег.Оператор.Назначение ELSE Прив.Владелец.Наименование END AS Владелец
	|FROM	РегистрСведений.ЗвонкиМаршруты Рег
	|LEFT	JOIN РегистрСведений.ОбъектыПривязка Прив ON (Прив.Объект = Рег.Оператор)
	|WHERE	(Рег.Сигнатура = &Сигнатура)
	|ORDER	BY Рег.НомерСобытия";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Сигнатура",ВРег(СокрЛП(Запись.Сигнатура)));
	Результат = Запрос.Выполнить().Выбрать();
	ЭтаФорма.Маршрут.Очистить();

	Пока (Результат.Следующий()) Цикл
		НовСтрока 					= ЭтаФорма.Маршрут.Добавить();
		НовСтрока.Дата 				= Результат.Дата;
		НовСтрока.Оператор 			= СокрЛП(Результат.Оператор);
		НовСтрока.Владелец		 	= СокрЛП(Результат.Владелец);
		НовСтрока.Событие 			= СокрЛП(Результат.ИмяСобытия);
		НовСтрока.ТочкаРазмещения 	= СокрЛП(Результат.ТочкаРазмещения);
	КонецЦикла;
КонецПроцедуры

//-------------------------------------------------------------------------------------------------

&НаКлиенте
Процедура СигнатураОткрытие(Элемент,СтандартнаяОбработка)

// Откроем форму звонка
//-------------------------------------------------------------------------------------------------
	Попытка
		СтрКлюча = Новый Структура("Сигнатура",СокрЛП(Запись.Сигнатура));
		КлючЗаписи = ЗначенияСервера.ПолучитьКлючЗаписи("Звонки",СтрКлюча);
		ОткрытьЗначение(КлючЗаписи);
	Исключение
	КонецПопытки;

	СтандартнаяОбработка = Ложь;
КонецПроцедуры
