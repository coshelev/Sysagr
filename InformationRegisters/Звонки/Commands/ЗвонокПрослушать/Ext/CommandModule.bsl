&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды,ПараметрыВыполненияКоманды)

// Работоспособность команды гарантируется только в случае если она вызвана из формы записи звонка
// Команда открывает файл с записью разговора звонка в медиа-плейере по-умолчанию
//-------------------------------------------------------------------------------------------------
	Приемник = ПолучитьИмяВременногоФайла("WAV");
	Сигнатура = Неопределено;
	Источник = Неопределено;

// Попытка № 1 получить сигнатуру и путь к файлу с записью звонка открытого в вызывающей форме
//-------------------------------------------------------------------------------------------------
	Попытка
		Сигнатура = СокрЛП(ПараметрыВыполненияКоманды.Источник.Запись.Сигнатура);
		Источник = Звонки.ЗаписьРазговораПолучить(Сигнатура);
	Исключение
	КонецПопытки;

// Если Сигнатура или путь к файлу не заполнены, значит прервем обработку
//-------------------------------------------------------------------------------------------------
	Доступно = ЗначениеЗаполнено(Сигнатура);
	Доступно = Доступно И ЗначениеЗаполнено(Источник);

// Попытка №2 определить сигнатуру (сработает для формы СписокИсходящихЗвонков)	
//-------------------------------------------------------------------------------------------------
	Если (Доступно = Ложь) Тогда
		Попытка
			Сигнатура = СокрЛП(ПараметрыВыполненияКоманды.Источник.Элементы.Список.ТекущиеДанные.Сигнатура);
			Источник = Звонки.ЗаписьРазговораПолучить(Сигнатура);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Доступно = ЗначениеЗаполнено(Сигнатура);
	Доступно = Доступно И ЗначениеЗаполнено(Источник);
	Если (Доступно = Ложь) Тогда
		Предупреждение("Файл с записью разговора не найден");
		Возврат;
	КонецЕсли;

// С помощью сервера получим файл во временное хранилище на клиента (таким образом у текущего
// пользователя отпадает необходимость в настройке прав доступа к серверным каталогам с файлами)
//-------------------------------------------------------------------------------------------------
	Попытка
		МассивОписаний = Новый Массив;
		АдресФайлаВХранилище = ФайлЗагрузитьВХранилище(Источник);
		МассивОписаний.Добавить(Новый ОписаниеПередаваемогоФайла(Приемник,АдресФайлаВХранилище));
		ПолучитьФайлы(МассивОписаний,,,Ложь);
		ЗапуститьПриложение(Приемник);
	Исключение
		ТекстСообщения = "Не удалось получить файл с записью разговора по ";
		ТекстСообщения = ТекстСообщения + "причине: " + СокрЛП(ОписаниеОшибки());
		Предупреждение(ТекстСообщения);
	КонецПопытки;
КонецПроцедуры

&НаСервере
Функция ФайлЗагрузитьВХранилище(Источник)
	ДанныеФайла = Новый ДвоичныеДанные(Источник);
	Ответ = ПоместитьВоВременноеХранилище(ДанныеФайла);
	Возврат (Ответ);
КонецФункции
