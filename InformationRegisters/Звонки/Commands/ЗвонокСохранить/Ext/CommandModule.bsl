&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды,ПараметрыВыполненияКоманды)

// Работоспособность команды гарантируется только в случае если она вызвана из формы записи звонка
// Команда копирует файл с записью разговора звонка из хранилища сервера в локальный файл клиента
//-------------------------------------------------------------------------------------------------
	Источник = Неопределено;
	Звонок = Неопределено;

// Получим сигнатуру и путь к файлу с записью звонка открытого в вызывающей форме
//-------------------------------------------------------------------------------------------------
	Попытка
		Звонок = ПараметрыВыполненияКоманды.Источник.Запись;
		Источник = Звонки.ЗаписьРазговораПолучить(Звонок.Сигнатура);
	Исключение
	КонецПопытки;

// Если путь к файлу не получен, значит прервем обработку
//-------------------------------------------------------------------------------------------------
	Если (НЕ ЗначениеЗаполнено(Источник)) Тогда
		Предупреждение("Файл с записью разговора не найден");
		Возврат;
	КонецЕсли;

// Сформируем имя файла по-умолчанию
//-------------------------------------------------------------------------------------------------
	
	Доступно = Звонок.Свойство("АбонентВнешний");
	Доступно = Доступно И Звонок.Свойство("АбонентВнутренний");
	Доступно = Доступно И Звонок.Свойство("ЭтоВходящий");
	Доступно = Доступно И Звонок.Свойство("Дата");
	
	Если Доступно Тогда
		ВнешНомер = ?(ЗначениеЗаполнено(Звонок.АбонентВнешний),СокрЛП(Звонок.АбонентВнешний),"НЕИЗВЕСТНЫЙ");
		ВнутНомер = ?(ЗначениеЗаполнено(Звонок.АбонентВнутренний),СокрЛП(Звонок.АбонентВнутренний),"НЕИЗВЕСТНЫЙ");
		
		Если (Звонок.ЭтоВходящий = Истина) Тогда
			ИмяФайла = "Входящий от " + СокрЛП(Формат(Звонок.Дата,"ДФ=дд-ММ-гггг")) + " с номера ";
			ИмяФайла = ИмяФайла + СокрЛП(ВнешНомер) + " на номер " + СокрЛП(ВнутНомер) + ".wav";
		Иначе
			ИмяФайла = "Исходящий от " + СокрЛП(Формат(Звонок.Дата,"ДФ=дд-ММ-гггг")) + " с номера ";
			ИмяФайла = ИмяФайла + СокрЛП(ВнутНомер) + " на номер " + СокрЛП(ВнешНомер) + ".wav";
		КонецЕсли;
	Иначе
		ИмяФайла = Звонок.Сигнатура;
	КонецЕсли;

// Запросим у пользователя путь для сохранения файла
//-------------------------------------------------------------------------------------------------
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.Заголовок = "Сохранение файла с записью звонка";
	ДиалогВыбора.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбора.ПолноеИмяФайла = СокрЛП(ИмяФайла);
	ДиалогВыбора.Расширение = "WAV";

// С помощью сервера скопируем файл в локальную папку клиента (таким образом у пользователя
// отпадает необходимость в настройке прав доступа к серверным каталогам с файлами)
//-------------------------------------------------------------------------------------------------
	Если (ДиалогВыбора.Выбрать() = Истина) Тогда
		Попытка
			МассивОписаний = Новый Массив;
			АдресФайлаВХранилище = ФайлЗагрузитьВХранилище(Источник);
			МассивОписаний.Добавить(Новый ОписаниеПередаваемогоФайла(ДиалогВыбора.ПолноеИмяФайла,АдресФайлаВХранилище));
			ПолучитьФайлы(МассивОписаний,,,Ложь);
		Исключение
			ТекстСообщения = "Не удалось получить файл с записью разговора по ";
			ТекстСообщения = ТекстСообщения + "причине: " + СокрЛП(ОписаниеОшибки());
			Предупреждение(ТекстСообщения);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ФайлЗагрузитьВХранилище(Источник)
	ДанныеФайла = Новый ДвоичныеДанные(Источник);
	Ответ = ПоместитьВоВременноеХранилище(ДанныеФайла);
	Возврат (Ответ);
КонецФункции
