&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Сформируй список причин ручного закрытия
	//-----------------------------------------
	СформируйСписокПричинРучногоЗакрытия();
КонецПроцедуры

&НаСервере
Процедура СформируйСписокПричинРучногоЗакрытия()
	Запрос = Новый Запрос("ВЫБРАТЬ Т.Ответ КАК Ответ ИЗ Справочник.Анкеты.ВариантыОтветов КАК Т ГДЕ Т.Ссылка.Наименование = &Наименование");
	Запрос.УстановитьПараметр("Наименование",	"ЗАДАЧИ ЛОЯЛЬНОСТИ: ПРИЧИНА РУЧНОГО ЗАКРЫТИЯ");
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.ПричинаРучногоЗакрытия.СписокВыбора.Добавить(Выборка.Ответ);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Обработать(Команда)
	Рез = ОбработатьНаСервере(Анкета, ПричинаРучногоЗакрытия);
	Сообщ = Новый СообщениеПользователю();
	Сообщ.Текст = ?(СтрНайти(Рез, "ошибка") > 0, Рез, "Выполнено успешно");
	Сообщ.Сообщить();
КонецПроцедуры

&НаСервере
Функция ОбработатьНаСервере(Анкета, Причина)
	
	Доступно = 		ТипЗнч(Анкета) 	= Тип("Строка")
				И 	ТипЗнч(Причина) = Тип("Строка")
				И	ЗначениеЗаполнено(Анкета)
				И	ЗначениеЗаполнено(Причина);
	Если Не Доступно Тогда
		Возврат "ошибка: не верные параметры";
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 СигнатураЗадачQ ИЗ РегистрСведений.АнкетыРасширенные ГДЕ СигнатураЗадачQ = &Анкета");
	Запрос.УстановитьПараметр("Анкета", Анкета);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "ошибка: не найдена анкета";
	КонецЕсли;
	Рез = РегистрыСведений.ЗадачиЗвонокЛояльности.ПерезакройЗакрытуюЗадачуПричинойРучногоЗакрытия(Анкета, ЗначенияСервера.ТекущаяДатаСервер(), Причина, ПараметрыСеанса.ТекущийПользователь);
	Возврат Рез;
КонецФункции




