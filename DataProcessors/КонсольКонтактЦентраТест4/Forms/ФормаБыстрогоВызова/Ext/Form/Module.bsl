&НаСервере
Процедура ПриСозданииНаСервере(Отказ,СтандартнаяОбработка)

// Определим параметры для формирования панели
// Параметры переданные в форму при вызове имеют приоритет над параметрами сеанса
//-------------------------------------------------------------------------------------------------
	КЦСсылка = ЭтаФорма.Параметры.КонтактЦентр;
	КЦСсылка = ?(ЗначениеЗаполнено(КЦСсылка),КЦСсылка,ПараметрыСеанса.ККЦКонтактЦентр);
	Элементы.ГруппаРекомендуемая.Видимость = Ложь;
	РекомендуемаяОчередь = Неопределено;

	Если (НЕ ЗначениеЗаполнено(КЦСсылка)) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

// Если передан параметр "Телефон", значит есть рекламный канал. Попытаемся для переданного канала
// и контакт-центра определить рекомендуемую для вызова системную очередь
//-------------------------------------------------------------------------------------------------
	Если (ЗначениеЗаполнено(ЭтаФорма.Параметры.Телефон)) Тогда
		ТекСтрока = ЭтаФорма.Параметры.Телефон.Распределение.Найти(КЦСсылка,"КонтактЦентр");
		РекомендуемаяОчередь = ?(ТекСтрока = Неопределено,РекомендуемаяОчередь,ТекСтрока.Очередь);

		Элементы.РекомендуемаяRedirect.Заголовок = СокрЛП(РекомендуемаяОчередь.Назначение);
		Элементы.РекомендуемаяRedirect.Подсказка = СокрЛП(РекомендуемаяОчередь.Код);
		Элементы.ГруппаРекомендуемая.Видимость = Истина;
	КонецЕсли;

// Создадим таблицу значений, в которую поместим все найденные для контакт-центра панели
//-------------------------------------------------------------------------------------------------
	ТЗПанели = Новый ТаблицаЗначений;
	ТЗПанели.Колонки.Добавить("Ссылка");
	ТЗПанели.Колонки.Добавить("Порядок");

// Найдем панели для контакт-центра
//-------------------------------------------------------------------------------------------------
	Выборка = Справочники.КонтактЦентры.Выбрать(КЦСсылка);
	ТекущийПорядок = 100;

	Пока (Выборка.Следующий()) Цикл
		НовСтрока = ТЗПанели.Добавить();
		НовСтрока.Ссылка = Выборка.Ссылка;
		НовСтрока.Порядок = ТекущийПорядок;
		ТекущийПорядок = ТекущийПорядок + 1;

// Если переменная "РекомендуемаяОчередь" заполнена, то попытаемся найти ссылку на эту очередь в
// выбранных панелях. Если в какой-либо панели эта очередь найдена, то объявим панель приоритетной
//-------------------------------------------------------------------------------------------------
		Если (ЗначениеЗаполнено(РекомендуемаяОчередь)) Тогда
			НайденнаяСтрока = Выборка.ПанельСостав.Найти(РекомендуемаяОчередь,"Телефон");
			НовСтрока.Порядок = ?(НайденнаяСтрока = Неопределено,НовСтрока.Порядок,0);
		КонецЕсли;
	КонецЦикла;

// Отсортируем полученный список элементов панели и выведем их на форму
//-------------------------------------------------------------------------------------------------
	ТЗПанели.Сортировать("Порядок");

	Для Каждого ТекПанель Из ТЗПанели Цикл
		СоздатьПанель(ТекПанель.Ссылка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СоздатьПанель(ТекПанель)

// Создадим группу на форме
//-------------------------------------------------------------------------------------------------
	Группа = Элементы.Добавить("Группа" + СокрЛП(ТекПанель.Код),Тип("ГруппаФормы"),ЭтаФорма);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;

	Группа.Отображение = ОтображениеОбычнойГруппы.СильноеВыделение;
	Группа.Заголовок = СокрЛП(ТекПанель.Наименование);
	Группа.РастягиватьПоГоризонтали = Истина;
	Группа.ОтображатьЗаголовок = Истина;
	НомерСтроки = 1;

// Создадим строки в группе. Каждая строка состоит из трех элементов
// - Картинка (очередь или телефон)
// - Гиперссылка для перевода без уведомления
// - Гиперссылка для перевода с уведомлением
//-------------------------------------------------------------------------------------------------
	Для Каждого ТекСтрока Из ТекПанель.ПанельСостав Цикл
		ИмяЭлемента = СокрЛП(Группа.Имя) + "_" + СокрЛП(Формат(НомерСтроки,"ЧГ=0"));
		НовСтрока = Элементы.Добавить(ИмяЭлемента,Тип("ГруппаФормы"),Группа);
		НовСтрока.Вид = ВидГруппыФормы.ОбычнаяГруппа;

		НовСтрока.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		НовСтрока.Отображение = ОтображениеОбычнойГруппы.Нет;
		НовСтрока.ОтображатьЗаголовок = Ложь;
		НомерСтроки = НомерСтроки + 1;

// Картинка
//-------------------------------------------------------------------------------------------------
		НовЭлемент = Элементы.Добавить(СокрЛП(НовСтрока.Имя) + "_Картинка",Тип("ДекорацияФормы"),НовСтрока);
		НовЭлемент.Вид = ВидДекорацииФормы.Картинка;

		ЭтоОчередь = (ТипЗнч(ТекСтрока.Телефон) = Тип("СправочникСсылка.ТелОчереди"));
		ИмяКартинки = ?(ЭтоОчередь,"ВыборКомпоновкиДанных","Пользователь");
		НовЭлемент.Картинка = БиблиотекаКартинок[ИмяКартинки];
		НовЭлемент.Ширина = 2;
		НовЭлемент.Высота = 1;

// Redirect
//-------------------------------------------------------------------------------------------------
		НовЭлемент = Элементы.Добавить(СокрЛП(НовСтрока.Имя) + "_Redirect",Тип("ДекорацияФормы"),НовСтрока);
		НовЭлемент.Вид = ВидДекорацииФормы.Надпись;

		НовЭлемент.УстановитьДействие("Нажатие","ГиперссылкаНажатие");
		НовЭлемент.Заголовок = СокрЛП(ТекСтрока.Наименование);
		НовЭлемент.Подсказка = СокрЛП(ТекСтрока.Телефон.Код);
		НовЭлемент.ЦветТекста = Новый Цвет(0,0,255);
		НовЭлемент.Гиперссылка = Истина;

// Atxfer
//-------------------------------------------------------------------------------------------------
		НовЭлемент = Элементы.Добавить(СокрЛП(НовСтрока.Имя) + "_Atxfer",Тип("ДекорацияФормы"),НовСтрока);
		НовЭлемент.Вид = ВидДекорацииФормы.Надпись;

		НовЭлемент.УстановитьДействие("Нажатие","ГиперссылкаНажатие");
		НовЭлемент.Подсказка = СокрЛП(ТекСтрока.Телефон.Код);
		НовЭлемент.ЦветТекста = Новый Цвет(0,128,0);
		НовЭлемент.Заголовок = "С уведомлением";
		НовЭлемент.Гиперссылка = Истина;
	КонецЦикла;
КонецПроцедуры

//-------------------------------------------------------------------------------------------------

&НаКлиенте
Процедура ГиперссылкаНажатие(Элемент)

// Если у переданного элемента заполнен реквизит "Подсказка", то выполним переключение звонка
// на номер содержащийся в подсказке
//-------------------------------------------------------------------------------------------------
	Доступно = ЗначениеЗаполнено(ЭтаФорма.Параметры.Канал);
	Доступно = Доступно И ЗначениеЗаполнено(Элемент.Подсказка);

	Если (Доступно = Истина) Тогда
		Если (Найти(ВРег(Элемент.Имя),"ATXFER") > 0) Тогда
			КонтактЦентр.КомандаAtxfer(ЭтаФорма.Параметры.Канал,Элемент.Подсказка);
		Иначе
			КонтактЦентр.КомандаBlindTransfer(ЭтаФорма.Параметры.Канал,Элемент.Подсказка);
		КонецЕсли;

		Оповестить("Перевод",Элемент.Подсказка,ЭтаФорма);
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры
