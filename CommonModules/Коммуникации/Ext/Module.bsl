
Функция АгрегаторМобильныйПолучить(ТелефонНомер,АгрКлиент = Неопределено) Экспорт

// Функция возвращает структуру с информацией о мобильном номере телефона связанном с переданным
// внутренним номером. В случае неудачи возвращается Неопределенность
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ТелефонНомер) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(ТелефонНомер);

	Если (Доступно = Ложь) Тогда
		Возврат Неопределено;
	КонецЕсли;    
	
	Возврат Неопределено;
КонецФункции


//*************************************************************************************************
// Работа с телефонией
//*************************************************************************************************

Функция АбонентВызвать(НомерИсточник, НомерПриемник, ДопПарам = "") Экспорт

// Функция инициирует телефонный вызов от оператора (НомерИсточник) на абонента (НомерПриемник)
// - НомерИсточник (строка) - номер внутреннего телефона оператора (вызывающий оператор)
// - НомерПриемник (строка) - номер телефона абонента (вызываемый номер)
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(НомерИсточник) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(НомерИсточник);

	Если (Доступно = Ложь) Тогда
		Возврат ("АбонентВызвать: не передан номер телефона оператора");
	КонецЕсли;

// Убедимся в том, что номер телефона-приемника передан корректно
//-------------------------------------------------------------------------------------------------
	ТелПриемник = Конвертация.ТелефонДляВызоваПолучить(НомерПриемник);
	Ответ = "АбонентВызвать: ошибка обращения к серверу телефонии";

	Если (НЕ ЗначениеЗаполнено(ТелПриемник)) Тогда
		Возврат ("АбонентВызвать: номер телефона вызываемого абонента не передан, либо является некорректным");
	КонецЕсли;

// Инициируем вызов
//-------------------------------------------------------------------------------------------------
	Попытка
	    ОпределениеСервиса = Новый WSОпределения("http://astws.main.luidorauto.ru:8081/asterisk_ws/soap/description");
		ВебСервис = Новый WSПрокси(ОпределениеСервиса, "http://tempuri.org/", "asterisk_ws", "asterisk_ws"); 
		Результат = ВебСервис.make_call(ВРег(СокрЛП(НомерИсточник)),ВРег(СокрЛП(ТелПриемник)), ДопПарам);
		Ответ = ?(ТипЗнч(Результат) = Тип("Строка"),Результат,Ответ);
	Исключение
	КонецПопытки;

	Возврат (СокрЛП(Ответ));
КонецФункции

//-------------------------------------------------------------------------------------------------

Функция ОчередьДобавитьАгента(ТелефонНомер,ОчередьИмя) Экспорт

// Функция выполняет включение переданного агента в переданную очередь (в неактивном состоянии)
// При успешном выполнении функция возвращает пустую строку. Иначе - строку с описанием ошибки
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ТелефонНомер) = Тип("Строка"));
	Доступно = Доступно И (ТипЗнч(ОчередьИмя) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(ТелефонНомер);
	Доступно = Доступно И ЗначениеЗаполнено(ОчередьИмя);
	Ответ = "";

	Если (Доступно = Истина) Тогда
		Попытка
			ОпределениеСервиса = Новый WSОпределения("http://astws.main.luidorauto.ru:8081/asterisk_ws/soap/description");
			ВебСервис = Новый WSПрокси(ОпределениеСервиса, "http://tempuri.org/", "asterisk_ws", "asterisk_ws"); 
			Ответ = ВебСервис.add_agent(СокрЛП(ТелефонНомер),ВРег(СокрЛП(ОчередьИмя)));
		Исключение
		КонецПопытки;
	КонецЕсли;

	Ответ = ?(ТипЗнч(Ответ) = Тип("Строка"),Ответ,"");
	Возврат (СокрЛП(Ответ));
КонецФункции

Функция ОчередиУдалитьАгента(ТелефонНомер) Экспорт

// Функция выполняет удаление переданного агента изо всех доступных ему очередей
// В случае успеха возвращает пустую строку. В случае неудачи - строку с текстом ошибки
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ТелефонНомер) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(ТелефонНомер);
	Ответ = Неопределено;

	Если (Доступно = Истина) Тогда
		Попытка
			ОпределениеСервиса = Новый WSОпределения("http://astws.main.luidorauto.ru:8081/asterisk_ws/soap/description");
			ВебСервис = Новый WSПрокси(ОпределениеСервиса, "http://tempuri.org/", "asterisk_ws", "asterisk_ws"); 
			Ответ = ВебСервис.remove_agent(СокрЛП(ТелефонНомер));
		Исключение
		КонецПопытки;
	КонецЕсли;

	Ответ = ?(ТипЗнч(Ответ) = Тип("Строка"),Ответ,"");
	Возврат (СокрЛП(Ответ));
КонецФункции

//-------------------------------------------------------------------------------------------------

Функция АгентСостояниеИнвертировать(ТелефонНомер) Экспорт

// Функция инвертирует состояние активности для переданного номера телефона
// В случае успеха возвращает пустую строку. В случае неудачи - строку с текстом ошибки
//-------------------------------------------------------------------------------------------------
	ТекСостояние = АгентСостояниеПолучить(ТелефонНомер);
	Ответ = "Не удалось получить текущее состояние агента";

	Если (ТекСостояние <> Неопределено) Тогда
		Попытка
			НовСостояние = ?(ТекСостояние.Активность = Истина,Ложь,Истина);
			ОпределениеСервиса = Новый WSОпределения("http://astws.main.luidorauto.ru:8081/asterisk_ws/soap/description");
			ВебСервис = Новый WSПрокси(ОпределениеСервиса, "http://tempuri.org/", "asterisk_ws", "asterisk_ws"); 
			Ответ = ВебСервис.change_agent_state("",ТекСостояние.Телефон,НовСостояние,0);
		Исключение
		КонецПопытки;
	КонецЕсли;

	Ответ = ?(ТипЗнч(Ответ) = Тип("Строка"),Ответ,"");
	Возврат (СокрЛП(Ответ));
КонецФункции

Функция АгентСостояниеПолучить(ТелефонНомер) Экспорт

// Функция возвращает структуру с информацией об агенте с переданным номером телефона:
// - Активность (булево) - состояние активности (Истина - активен, Ложь - пауза)
// - Телефон (строка) - номер внутреннего телефона агента
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(ТелефонНомер) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(ТелефонНомер);
	Ответ = Неопределено;

	Если (Доступно = Истина) Тогда
		Попытка
			ВебСервис = WSСсылки.PbxService.СоздатьWSПрокси("http://tempuri.org/","asterisk_ws","asterisk_ws");
			Результат = ВебСервис.get_agent_state(СокрЛП(ТелефонНомер));
		Исключение
		КонецПопытки;

// Обработаем полученный результат
//-------------------------------------------------------------------------------------------------
		Доступно = (ТипЗнч(Результат) = Тип("Строка"));
		Доступно = Доступно И (СтрДлина(СокрЛП(Результат)) = 1);
		Результат = ?(Доступно = Истина,ВРег(СокрЛП(Результат)),"");

		Если (ЗначениеЗаполнено(Результат)) Тогда
			Ответ = Новый Структура;
			Ответ.Вставить("Телефон",СокрЛП(ТелефонНомер));
			Ответ.Вставить("Активность",(Результат = "A"));
		КонецЕсли;
	КонецЕсли;

	Возврат (Ответ);
КонецФункции
