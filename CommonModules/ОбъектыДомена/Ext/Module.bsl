Функция ЗапросВыполнить(ТекстЗапроса)

// Функция выполняет запрос к LDAP://MAIN.LUIDORAUTO.RU
// Если запрос выполнен успешно, то возвращается COM-Объект - выборка ADODB
// В противном случае возвращается строка с описанием ошибки
//-------------------------------------------------------------------------------------------------
	Попытка
		АДОКоннектор = Новый COMОбъект("ADODB.Connection");
		АДОКоннектор.ConnectionString = "Provider=""ADsDSOObject""";
		АДОКоннектор.Open();

		Ответ = АДОКоннектор.Execute(СокрЛП(ТекстЗапроса));
	Исключение
		Возврат ("ОбъектыДомена: Ошибка при выполнении запроса к LDAP: " + СокрЛП(ОписаниеОшибки()));
	КонецПопытки;

// Запрос выполнен
//-------------------------------------------------------------------------------------------------
	Если (ТипЗнч(Ответ) = Тип("COMОбъект")) Тогда
		Возврат (Ответ);
	КонецЕсли;

	Возврат ("ОбъектыДомена: Неизвестная ошибка при выполнении запроса к LDAP");
КонецФункции

Функция ПользовательПолучитьПоИмени(СтрокаПоиска) Экспорт

// Функция возвращает COM-Объект типа "УчетнаяЗаписьПользователя"
// Поиск в Active Directory выполняется по имени пользователя (DisplayName)
// В случае успеха функция возвращает COM-Объект. В случае неудачи - строку с описанием ошибки
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(СтрокаПоиска) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(СтрокаПоиска);

	Если (Доступно = Ложь) Тогда
		Возврат ("ОбъектыДомена: Не передано имя пользователя для поиска");
	КонецЕсли;

// Сформируем текст запроса и выполним запрос
//-------------------------------------------------------------------------------------------------
	ТекстЗапроса = "SELECT AdsPath FROM 'LDAP://MAIN.LUIDORAUTO.RU' ";
	ТекстЗапроса = ТекстЗапроса + "WHERE objectCategory = 'user' AND objectClass = 'person' AND ";
	ТекстЗапроса = ТекстЗапроса + "DisplayName = '" + СокрЛП(СтрокаПоиска) + "'";
	Результат = ЗапросВыполнить(ТекстЗапроса);

// Если полученный результат не является COM-Объектом, то вернем ошибку
//-------------------------------------------------------------------------------------------------
	Если (ТипЗнч(Результат) <> Тип("COMОбъект")) Тогда
		Возврат (Результат);
	КонецЕсли;

// Результат содержит COM-Объект - выборку
//-------------------------------------------------------------------------------------------------
	Если (НЕ Результат.EOF) Тогда
		ПутьОбъекта = Результат.Fields("AdsPath").Value;
		Возврат (ПолучитьCOMОбъект(ПутьОбъекта));
	КонецЕсли;

	Возврат ("ОбъектыДомена: Пользователь с переданным именем не найден в LDAP");
КонецФункции

Функция ПользовательПолучитьПоУчетнойЗаписи(СтрокаПоиска) Экспорт

// Функция возвращает COM-Объект типа "УчетнаяЗаписьПользователя"
// Поиск в Active Directory выполняется по учетной записи пользователя (sAMAccountName)
// В случае успеха функция возвращает COM-Объект. В случае неудачи - строку с описанием ошибки
//-------------------------------------------------------------------------------------------------
	Доступно = (ТипЗнч(СтрокаПоиска) = Тип("Строка"));
	Доступно = Доступно И ЗначениеЗаполнено(СтрокаПоиска);

	Если (Доступно = Ложь) Тогда
		Возврат ("ОбъектыДомена: Не передано имя учетной записи для поиска");
	КонецЕсли;

// Сформируем текст запроса и выполним запрос
//-------------------------------------------------------------------------------------------------
	ТекстЗапроса = "SELECT AdsPath FROM 'LDAP://MAIN.LUIDORAUTO.RU' ";
	ТекстЗапроса = ТекстЗапроса + "WHERE objectCategory = 'user' AND objectClass = 'person' AND ";
	ТекстЗапроса = ТекстЗапроса + "sAMAccountName = '" + СокрЛП(СтрокаПоиска) + "'";
	Результат = ЗапросВыполнить(ТекстЗапроса);

// Если полученный результат не является COM-Объектом, то вернем ошибку
//-------------------------------------------------------------------------------------------------
	Если (ТипЗнч(Результат) <> Тип("COMОбъект")) Тогда
		Возврат (Результат);
	КонецЕсли;

// Результат содержит COM-Объект - выборку
//-------------------------------------------------------------------------------------------------
	Если (НЕ Результат.EOF) Тогда
		ПутьОбъекта = Результат.Fields("AdsPath").Value;
		Возврат (ПолучитьCOMОбъект(ПутьОбъекта));
	КонецЕсли;

	Возврат ("ОбъектыДомена: Учетная запись с переданным именем не найдена в LDAP");
КонецФункции
